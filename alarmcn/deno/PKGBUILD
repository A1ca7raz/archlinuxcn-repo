# Contributor: Beno√Æt Zugmeyer <bzugmeyer@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=deno
pkgver=1.38.1
pkgrel=1
pkgdesc="A secure runtime for JavaScript and TypeScript"
arch=('aarch64' 'armv7h' 'i686' 'x86_64')
url="https://deno.land"
license=('MIT')
makedepends=('python' 'cargo' 'nodejs' 'cmake' 'protobuf')
source=("https://github.com/denoland/deno/releases/download/v${pkgver}/deno_src.tar.gz")
sha256sums=('6b698b1ad599057ae4acaa21214b98e3b8c6214ef88dedfa9d7eefe242d7a477')
# The build system does not like LTO... (symbol not found error during linking)
options=('!lto')

build() {
  cd deno
  cargo build --release
}

check() {
  cd deno
  ./target/release/deno run cli/tests/testdata/run/002_hello.ts
}

package() {
  cd deno

  install -Dm755 target/release/deno "$pkgdir"/usr/bin/deno
  install -dm755 "$pkgdir"/usr/share/bash-completion/completions
  ./target/release/deno completions bash > "$pkgdir"/usr/share/bash-completion/completions/deno
  install -dm755 "$pkgdir"/usr/share/zsh/site-functions
  ./target/release/deno completions zsh > "$pkgdir"/usr/share/zsh/site-functions/_deno
  install -dm755 "$pkgdir"/usr/share/fish/vendor_functions.d
  ./target/release/deno completions fish > "$pkgdir"/usr/share/fish/vendor_functions.d/deno.fish
  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
