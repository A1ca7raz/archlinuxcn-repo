pkgname=libllvmextra_jll
pkgver=7.2.1
_commit=9ba797aaefebb8983f3f3bacc80feaf0b4764d7f
pkgrel=1
pkgdesc="LLVM.jl"
url="https://github.com/maleadt/LLVM.jl.git"
arch=(aarch64 armv7h x86_64)
license=('MIT')
makedepends=(git cmake llvm-julia-git)
source=("git+https://github.com/maleadt/LLVM.jl.git#commit=$_commit")
sha256sums=('7b3cdb9b03921a48147e1286a7ea765898e4183fff0cab31fc968d9e494d1ef9')
options=('debug')

_versioned_llvm=

build() {
  cd LLVM.jl/deps/LLVMExtra

  mkdir -p build
  cd build
  if [[ -n $_versioned_llvm ]]; then
    export CMAKE_PREFIX_PATH=/usr/lib/llvm$_versioned_llvm/
  fi
  cmake .. -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  if [[ -n $_versioned_llvm ]]; then
    _llvm_ver=$(llvm-config-${_versioned_llvm} --version)
  else
    _llvm_ver=$(llvm-config --version)
  fi
  [[ $_llvm_ver =~ ^([0-9]*).* ]]
  llvm_maj_ver=${BASH_REMATCH[1]}
  ((next_llvm_maj=llvm_maj_ver + 1))

  _deps=("llvm-libs-julia-git>=${llvm_maj_ver}.0"
         "llvm-libs-julia-git<${next_llvm_maj}.0")

  depends+=("${_deps[@]}")

  cd LLVM.jl/deps/LLVMExtra/build

  make install DESTDIR="${pkgdir}"

  mkdir "${pkgdir}/usr/lib/julia"
  mv "${pkgdir}/usr/lib/"libLLVMExtra-* "${pkgdir}/usr/lib/julia"
}
