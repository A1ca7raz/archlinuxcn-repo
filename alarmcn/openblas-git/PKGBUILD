# Maintainer: all3fox <all3fox [at] gmail __DoT_ com>
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Jiaxi Hu <sftrytry _AT_ gmail _DOT_ com>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>

pkgbase=openblas-git
pkgname=(openblas-git openblas64-git)
_pkgname=OpenBLAS
pkgver=0.3.24.1dev.0.63.g617294b9e
pkgrel=1
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD"
arch=('aarch64' 'x86_64')
url="https://www.openblas.net/"
license=('BSD')
makedepends=('git' 'cmake' 'perl' 'gcc-fortran')
options=(!emptydirs debug strip)
source=("git+https://github.com/xianyi/OpenBLAS")
md5sums=('SKIP')

pkgver() {
  cd OpenBLAS

  _last_tag=$(git describe --tags --abbrev=0)
  _commit_short=$(git rev-parse --short HEAD)
  _build_number=$(git rev-list --count HEAD "^$_last_tag")
  # openblas versions goes as 0.0.1 -> 0.0.1.dev -> 0.0.2
  # Change the .dev into .1dev so that it is treated as a newer version
  # after adding the .0.* suffix
  _ver=$(sed -ne 's/^VERSION\s*=\s*\([^[:space:]]*\).*/\1/p' Makefile.rule |
      sed -e 's/\.dev/.1dev/')

  printf "%s.0.%s.g%s" "$_ver" "$_build_number" "$_commit_short"
}

prepare() {
  cd OpenBLAS

  # patch -Np1 < ../.patch
}

build() {
  # Setting FC manually to avoid picking up f95 and breaking the cmake build
  # https://github.com/xianyi/OpenBLAS/issues/4072#issuecomment-1576388332

  _cmake_opts=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DGEMM_MULTITHREAD_THRESHOLD=50
    -DBUILD_SHARED_LIBS=ON
    -DBUILD_TESTING=OFF
    -DNO_AFFINITY=ON
    -DUSE_OPENMP=1
    -DUSE_THREAD=1
    -DNUM_THREADS=64
    -DDYNAMIC_ARCH=ON
    -DNO_WARMUP=1
  )
  export FC=gfortran
  export FFLAGS="-g -O3 -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"

  if [[ $CARCH = x86_64 ]]; then
    _cmake_opts=("${_cmake_opts[@]}" -DCORE=CORE2)
  fi

  cmake -B build -S $_pkgname "${_cmake_opts[@]}"
  cmake --build build

  cmake -B build64 -S $_pkgname "${_cmake_opts[@]}" -DINTERFACE64=1
  cmake --build build64
}

package_openblas-git() {
  provides=("openblas=$pkgver")
  conflicts=('openblas')

  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $_pkgname/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_openblas64-git() {
  provides=("openblas64=$pkgver")
  conflicts=('openblas64')

  pkgdesc+=" (64-bit integers)"
  DESTDIR="$pkgdir" cmake --install build64
  install -Dm644 $_pkgname/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd "$pkgdir"/usr/lib/
  ln -s libopenblas_64.so.${pkgver%%.*} libopenblas64_.so # Needed by julia
}
