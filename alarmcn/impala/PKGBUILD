# Maintainer: orhun <orhunparmaksiz@gmail.com>

pkgname=impala
pkgver=0.2.1
pkgrel=3
pkgdesc="TUI for managing wifi"
url="https://github.com/pythops/${pkgname}"
depends=('gcc-libs' 'iwd')
makedepends=('cargo')
arch=('x86_64' 'aarch64' 'riscv64')
license=('GPL-3.0-or-later')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('ee1dd2e43dfdd87c8e69b41131a10e76a03df3a2f4cbb75e840ca7f1bd18f786e3bc1e468966bda1c0d8caef2a3d688f3c57775bd7d2f0d8e277486f8e9184c9')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --release --frozen
}

check() {
	cd "${pkgname}-${pkgver}/"
	cargo test --frozen
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 Readme.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
