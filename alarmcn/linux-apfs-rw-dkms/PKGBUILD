# Maintainer: Melvyn <melvyn2@brcok.tk>
# Maintainer: HurricanePootis <hurricanepootis@protonmail.com>
# Forked from: aur/linux-apfs-dkms-git
# Contributor: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: ManU
# Forked from aur/linux-can-dkms
# Contributor: Kyle Manna <kyle(at)kylemanna(dot)com>

pkgname=linux-apfs-rw-dkms
_pkgname=${pkgname%-dkms}
epoch=1
pkgver=0.3.10
pkgrel=1
pkgdesc="Experimental APFS kernel module with Write support (DKMS)"
arch=('any')
url="https://github.com/linux-apfs/${_pkgname}"
license=('GPL-2.0-only')
depends=('dkms')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
	"version.patch")
sha256sums=('a426b123b666761a4a5a1417a1e2cb73ea86fefcfe605376d51b7677848f27e3'
            '8a2814889a0a8a016d45cc2d7c032aec4a9f6eaa0a8551166c0124ce35bf766c')

prepare() {
	cd "${_pkgname}-${pkgver}/"
	patch -p1 <"${srcdir}/version.patch"
}

package() {
	cd "${_pkgname}-${pkgver}/"
	dkms_version=$(grep PACKAGE_VERSION dkms.conf | sed -r 's#PACKAGE_VERSION="([0-9.]+)"#\1#')
	dkms_dir="${pkgdir}/usr/src/linux-apfs-rw-${dkms_version}/"
	install -d "${dkms_dir}"
	cp -dr --no-preserve=ownership * "${dkms_dir}"
}
