# Maintainer: Aviel Warschawski <mail@aviel.org>

pkgname=gdb-multiarch
_pkgname=${pkgname%-multiarch}
pkgver=14.2
pkgrel=1
pkgdesc="The GNU Debugger for all gdb supported architectures (i386/arm/mips...)"
arch=("i686" "x86_64" "aarch64")
url="http://www.gnu.org/software/${_pkgname}"
license=("GPL3")
makedepends=("texinfo" "python" "guile" "ncurses" "expat" "xz" "mpfr"
	"source-highlight" "boost" "readline")
depends=("ncurses" "libncursesw.so" "expat" "xz" "mpfr" "source-highlight" "gdb-common=${pkgver}"
	"readline" "libreadline.so" "guile" "python" "libelf")
source=(https://ftp.gnu.org/gnu/${_pkgname}/${_pkgname}-${pkgver}.tar.xz{,.sig})
sha256sums=('2d4dd8061d8ded12b6c63f55e45344881e8226105f4d2a9b234040efa5ce7772'
            'SKIP')

validpgpkeys=("F40ADB902B24264AA42E50BF92EDB04BFF325CF3") # Joel Brobecker <brobecker@adacore.com>
options=("!emptydirs" "!lto")

prepare() {
	cd "${_pkgname}-${pkgver}/"
	sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
	cd "${_pkgname}-${pkgver}/"

	mkdir -p build && cd build
	../configure \
		--enable-targets=all \
		--prefix=/build \
		--enable-languages=all \
		--enable-multilib \
		--enable-interwork \
		--with-system-readline \
		--disable-nls \
		--with-python=/usr/bin/python \
		--with-system-gdbinit="/etc/${_pkgname}/gdbinit"

	make
}

package() {
	cd "${_pkgname}-${pkgver}/build/"

	make DESTDIR="${pkgdir}" install

	# Following files conflict with "gdb" package
	install -Dm755 "${pkgdir}/build/bin/${_pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	rm -r "${pkgdir}/build/"
}
