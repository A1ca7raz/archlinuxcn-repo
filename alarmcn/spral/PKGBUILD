pkgname=spral
pkgver=2023.09.07
pkgrel=1
pkgdesc="Sparse Parallel Robust Algorithms Library"
arch=('aarch64' 'armv7h' 'x86_64')
url="https://github.com/ralna/spral"
license=('BSD')
makedepends=(gcc-fortran)
depends=('metis' 'lapack' 'hwloc')
source=("https://github.com/ralna/spral/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('dbf74191e95c284fb0036886ec9d5047e96f111daaa4c80945c840565b84ef9e')

build() {
  cd $pkgname-$pkgver
  CFLAGS+=' -fPIC'
  CPPFLAGS+=' -fPIC'
  CXXFLAGS+=' -fPIC'
  FFLAGS+=' -fPIC'
  FCFLAGS+=' -fPIC'
  export CFLAGS CPPFLAGS CXXFLAGS FFLAGS FCFLAGS
  ./autogen.sh
  ./configure --prefix=/usr

  make -j1 # Buggy build system
  gfortran -fPIC -shared -Wl,--whole-archive libspral.a -Wl,--no-whole-archive \
           -lgomp -llapack -lhwloc -lmetis -lstdc++ -o libspral.so
}

package () {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  rm "$pkgdir/usr/lib/libspral.a"
  install -Dm755 libspral.so "$pkgdir/usr/lib/libspral.so"
}
