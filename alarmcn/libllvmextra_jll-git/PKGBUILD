#

pkgname=libllvmextra_jll-git
pkgver=v6.2.1.r5.g6096371
pkgrel=1
pkgdesc='Deps for LLVM.jl'
arch=(aarch64 armv7h i686 x86_64)
url='https://github.com/maleadt/LLVM'
license=('MIT')
depends=(llvm-julia)
makedepends=(git cmake)
source=('git+https://github.com/maleadt/LLVM.jl')
sha1sums=('SKIP')
provides=('libllvmextra_jll')
conflicts=('libllvmextra_jll')
options=('debug')

_versioned_llvm=15

pkgver() {
  cd LLVM.jl

  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd LLVM.jl
}

build() {
  cd LLVM.jl/deps/LLVMExtra

  mkdir -p build
  cd build
  if [[ -n $_versioned_llvm ]]; then
    export CMAKE_PREFIX_PATH=/usr/lib/llvm15/
  fi
  cmake .. -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  if [[ -n $_versioned_llvm ]]; then
    _llvm_ver=$(llvm-config-${_versioned_llvm} --version)
  else
    _llvm_ver=$(llvm-config --version)
  fi
  [[ $_llvm_ver =~ ^([0-9]*).* ]]
  llvm_maj_ver=${BASH_REMATCH[1]}
  ((next_llvm_maj=llvm_maj_ver + 1))

  _deps=("llvm-libs-julia>=${llvm_maj_ver}.0"
         "llvm-libs-julia<${next_llvm_maj}.0")

  depends+=("${_deps[@]}")

  cd LLVM.jl/deps/LLVMExtra/build

  make install DESTDIR="${pkgdir}"

  mkdir "${pkgdir}/usr/lib/julia"
  mv "${pkgdir}/usr/lib/"libLLVMExtra-* "${pkgdir}/usr/lib/julia"
}
