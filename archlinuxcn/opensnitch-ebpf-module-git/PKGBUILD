# Maintainer: lsf

pkgname=opensnitch-ebpf-module-git
_pkgname=opensnitch
pkgver=1.6.3.r99.785500cd
pkgrel=1
pkgdesc="eBPF process monitor module for opensnitch"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/evilsocket/${_pkgname}"
license=('GPL-3.0-or-later')
makedepends=('git' 'clang' 'llvm' 'libelf'
	'bc' 'rsync' 'linux-headers' 'coreutils')
depends=("${_pkgname}")
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://github.com/evilsocket/${_pkgname}.git"
	"0001-Build-with-Archlinux.patch")
sha256sums=('SKIP'
            '446c029de1cbd39308575e346511d80ee160d2dea575d64fddc7835fc3040460')
options=('!strip') # we're stripping with llvm-strip

pkgver() {
	cd "${_pkgname}/"
	git describe --long | sed 's/^v//;s/-rc./rc/;s/\([^-]*-\)g/r\1/;s/-/./g'
}

prepare() {
	cd "${_pkgname}/ebpf_prog/"
	patch Makefile <"${srcdir}/0001-Build-with-Archlinux.patch"
}

build() {
	cd "${_pkgname}/ebpf_prog/"
	KERNEL_VER=$(pacman -Si linux-headers | grep Version | awk '{print $3}' | sed 's/.arch/-arch/')
	KERNEL_DIR="/usr/lib/modules/${KERNEL_VER}/build" KERNEL_HEADERS="${KERNEL_DIR}" ARCH="${arch}" make all
}

package() {
	install -Dm644 ${_pkgname}/ebpf_prog/${_pkgname}{,-{procs,dns}}.o -t "${pkgdir}/usr/lib/opensnitchd/ebpf/"
}
