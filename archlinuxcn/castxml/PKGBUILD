# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=castxml
pkgdesc="C-family abstract syntax tree XML output tool."
pkgver=0.6.5
pkgrel=1
arch=('x86_64')
url='https://github.com/CastXML/CastXML'
license=('Apache')
depends=(
  clang
  llvm-libs
)
makedepends=(
  cmake
  git
  llvm
  python-sphinx
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/CastXML/CastXML/archive/v${pkgver}.tar.gz")
sha512sums=('1f6759242fd6097fc97df34b1689ae6660e42d64717be90fa2d2f251847a465e26e17d6ff5318d82c47981005c826dad0fe65b3536b13d9192b9bccf61097f10')

build() {
  cmake -B "build" -S "${srcdir}/CastXML-${pkgver}" \
    -DCastXML_INSTALL_DOC_DIR=share/doc/castxml \
    -DCastXML_INSTALL_MAN_DIR=share/man \
    -DCLANG_LINK_CLANG_DYLIB=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DSPHINX_HTML=ON \
    -DSPHINX_MAN=ON
  make -C "build"
}

check() {
  cd "build"
  ctest -v
}

package(){
  cd "build"
  make DESTDIR="${pkgdir}" install
}
# vim:set ts=2 sw=2 et:

