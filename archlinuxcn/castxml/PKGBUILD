# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=castxml
pkgdesc="C-family abstract syntax tree XML output tool."
pkgver=0.6.8
pkgrel=1
arch=('x86_64')
url='https://github.com/CastXML/CastXML'
license=('Apache-2.0')
depends=(
  clang
  llvm-libs
)
makedepends=(
  cmake
  git
  llvm
  python-sphinx
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/CastXML/CastXML/archive/v${pkgver}.tar.gz")
sha512sums=('7645286ef7fe89ebd1d582a289f9446a96f9aa6db89539314cc2a77bd005ca203d0346f5b4f7d14b83baefc41e0a2a8e55585b429985777cff6c3c0ffc1ef6ce')

build() {
  cmake -B "build" -S "${srcdir}/CastXML-${pkgver}" \
    -DCastXML_INSTALL_DOC_DIR=share/doc/castxml \
    -DCastXML_INSTALL_MAN_DIR=share/man \
    -DCLANG_LINK_CLANG_DYLIB=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DSPHINX_HTML=ON \
    -DSPHINX_MAN=ON
  make -C "build"
}

check() {
  cd "build"
  ctest --verbose
}

package(){
  cd "build"
  make DESTDIR="${pkgdir}" install
}
# vim:set ts=2 sw=2 et:

