# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=castxml
pkgdesc="C-family abstract syntax tree XML output tool."
pkgver=0.6.2
pkgrel=1
arch=('x86_64')
url='https://github.com/CastXML/CastXML'
license=('Apache')
depends=(
  clang
  llvm-libs
)
makedepends=(
  cmake
  git
  llvm
  python-sphinx
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/CastXML/CastXML/archive/v${pkgver}.tar.gz")
sha512sums=('eec6495b8ed837031b5d41cb09b8f70c497c865fbef47bc51b5f296fb9db852ded0bd13c45c3f3017d7d1eb5976557634e52369a24ddbf1f2cf9705c321bac3b')

build() {
  cmake -B "build" -S "${srcdir}/CastXML-${pkgver}" \
    -DCastXML_INSTALL_DOC_DIR=share/doc/castxml \
    -DCastXML_INSTALL_MAN_DIR=share/man \
    -DCLANG_LINK_CLANG_DYLIB=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DSPHINX_HTML=ON \
    -DSPHINX_MAN=ON
  make -C "build"
}

check() {
  cd "build"
  ctest -v
}

package(){
  cd "build"
  make DESTDIR="${pkgdir}" install
}
# vim:set ts=2 sw=2 et:

