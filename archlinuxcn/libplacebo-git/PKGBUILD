# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=libplacebo-git
_pkgname=libplacebo
epoch=1
pkgver=6.338.0.r16.gcb6be980
pkgrel=1
pkgdesc='Reusable library for GPU-accelerated video/image rendering primitives'
url='https://github.com/haasn/libplacebo'
arch=('x86_64')
license=('LGPL2.1')
depends=('vulkan-icd-loader' 'libvulkan.so'
         'glslang'
         'lcms2' 'liblcms2.so'
         'shaderc' 'libshaderc_shared.so'
         'libunwind'
         'xxhash' 'libxxhash.so'
         # for demos
         'ffmpeg' 'libavcodec.so' 'libavutil.so' 'libavformat.so'
         'glfw'
         'sdl2' 'sdl2_image'
         'libdovi' 'libdovi.so'
         )
makedepends=('git' 'meson' 'ninja' 'vulkan-headers' 'python' 'python-jinja' 'python-markupsafe' 'glad')
checkdepends=(
              'ffmpeg'
              'dav1d'
              )
provides=('libplacebo' 'libplacebo.so')
conflicts=('libplacebo')
source=(
        'git+https://code.videolan.org/videolan/libplacebo.git'
        'git+https://github.com/Immediate-Mode-UI/Nuklear.git'
        'git+https://github.com/KhronosGroup/Vulkan-Headers.git'
        'git+https://github.com/fastfloat/fast_float.git'
        )
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        'SKIP')
options=('debug' 'strip')

pkgver() {
    cd ${srcdir}/libplacebo
    git describe --long | sed 's/^v//;s/-rc/rc/;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd ${srcdir}/libplacebo
    git submodule set-url demos/3rdparty/nuklear "${srcdir}/Nuklear"
    git -c protocol.file.allow=always submodule update --init \
        demos/3rdparty/nuklear
    git submodule set-url 3rdparty/Vulkan-Headers "${srcdir}/Vulkan-Headers"
    git -c protocol.file.allow=always submodule update --init \
        3rdparty/Vulkan-Headers
    git submodule set-url 3rdparty/fast_float "${srcdir}/fast_float"
    git -c protocol.file.allow=always submodule update --init \
        3rdparty/fast_float
}

build() {
    cd ${srcdir}/libplacebo

    arch-meson build \
        --buildtype=debugoptimized \
        -D default_library=shared \
        -D tests=true \
        -D demos=true  \
        -D d3d11=disabled
    ninja -C build
}

check() {
    cd ${srcdir}/libplacebo
    ninja -C build test
}

package() {
    cd ${srcdir}/libplacebo
    DESTDIR="${pkgdir}" ninja -C build install
    install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=4 sw=4 et:
