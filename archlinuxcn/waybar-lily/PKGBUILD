# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Brett Cornwall <ainola@archlinux.org>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=waybar-lily
pkgver=0.10.2
pkgrel=1
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors, with less modules and deps, for PulseAudio users'
arch=('x86_64')
url="https://github.com/Alexays/Waybar/"
license=('MIT')
depends=(
    'fmt'
    'gtk-layer-shell'
    'gtkmm3'
    'libdate-tz.so'
    'libdbusmenu-gtk3'
    'libjsoncpp.so'
    'libnl'
    'libpulse'
    'libsigc++'
    'libspdlog.so'
    'libxkbcommon'
    'wayland'
)
makedepends=(
    'catch2'
    'cmake'
    'meson'
    'python-packaging' # gdbus-codegen
    'scdoc' # For generating manpages
    'wayland-protocols'
)
backup=(
    etc/xdg/waybar/config.jsonc
    etc/xdg/waybar/style.css
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/Alexays/Waybar/archive/$pkgver.tar.gz")
sha256sums=('7ecccfe5d326d66987a32e77e02b825019d191670ab8e87412df01dd0913f9c0')
provides=(waybar=$pkgver)
conflicts=(waybar)

build() {
    cd "Waybar-$pkgver"
    meson setup --prefix=/usr \
          --buildtype=plain \
          --wrap-mode=nodownload \
          -Dcpp_std=c++20 \
          -Dexperimental=true \
          -Dcava=disabled \
          -Dtests=disabled \
          build
    meson compile -C build
}

package() {
    cd "Waybar-$pkgver"
    meson install -C build --destdir "$pkgdir"
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
