# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Mike Cuche <cuche AT mailbox.org>

pkgname=dosbox-x
pkgver=2024.03.01
pkgrel=1
pkgdesc="x86 emulator with builtin DOS, with patches with more features"
arch=(i686 x86_64 aarch64)
url="http://dosbox-x.com"
license=('GPL-2.0-or-later AND LGPL-2.0-or-later AND LGPL-2.1-or-later AND GPL-3.0-or-later')
depends=(
  'alsa-lib'
  'ffmpeg'
  'fluidsynth'
  'freetype2'
  'gcc-libs'
  'glibc'
  'hicolor-icon-theme'
  'libglvnd'
  'libpng'
  'libpcap'
  'libslirp'
  'libx11'
  'libxkbfile'
  'libxrandr'
  'sdl2'
  'sdl2_net'
  'zlib' 'libz.so'
)
makedepends=(
  glu
  nasm
)
conflicts=(dosbox-x-git)
source=(
  "https://github.com/joncampbell123/dosbox-x/archive/dosbox-x-v$pkgver.tar.gz"
  "$pkgname-fix-gcc14.patch"
)

sha256sums=('2a89d3196ddc15361f6dc7e6ab142bfe95945d93d527cfd6bacca1f7a401a513'
            'fa9c3ccca232fa9ad4b1c1f22bc8b6a6124d11ea69a38e917566f743fa765625')

prepare() {
  cd "dosbox-x-dosbox-x-v$pkgver"
  patch -Np1 -i ../"$pkgname-fix-gcc14.patch"
  autoreconf -fiv
}

build() {
  cd "dosbox-x-dosbox-x-v$pkgver"

  # FIXME: Workaround bug https://github.com/joncampbell123/dosbox-x/issues/3039
  export CFLAGS=${CFLAGS/-Werror=format-security/}
  export CXXFLAGS=${CXXFLAGS/-Werror=format-security/}

  ./configure \
    --prefix=/usr \
    --enable-sdl2 \
    --enable-xbrz \
    --enable-alsa-midi \
    --enable-avcodec
  make
}

package() {
  depends+=(
    'libasound.so'
    'libavcodec.so'
    'libavformat.so'
    'libavutil.so'
    'libswscale.so'
    'libfluidsynth.so'
    'libfreetype.so'
    'libpng16.so'
    'libpcap.so'
    'libslirp.so'
    'libz.so'
  )
  cd "dosbox-x-dosbox-x-v$pkgver"
  make DESTDIR="$pkgdir" install
}
# vim:set ts=2 sw=2 et:
