# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Mike Cuche <cuche AT mailbox.org>

pkgname=dosbox-x
pkgver=2024.03.01
pkgrel=1
pkgdesc="x86 emulator with builtin DOS, with patches with more features"
arch=(i686 x86_64 aarch64)
url="http://dosbox-x.com"
license=(GPL) # FIXME: waiting for upstream reply
depends=('glibc' 'gcc-libs' 'hicolor-icon-theme' 'physfs'
        'alsa-lib' 'libasound.so'
        'libglvnd' 'libGL.so'
        'libx11' 'libxrandr' 'libxkbfile'
        'zlib' 'libz.so'
        'freetype2' 'libfreetype.so'
        'ffmpeg' 'libavcodec.so' 'libavformat.so' 'libavutil.so' 'libswscale.so'
        'sdl2' 'sdl2_net'
        'libpng' 'libpng16.so'
        'libpcap' 'libpcap.so'
        'libslirp' 'libslirp.so'
        'fluidsynth' 'libfluidsynth.so'
        )
makedepends=(glu nasm)
conflicts=(dosbox-x-git)
source=(
  "https://github.com/joncampbell123/dosbox-x/archive/dosbox-x-v$pkgver.tar.gz"
)

sha256sums=('2a89d3196ddc15361f6dc7e6ab142bfe95945d93d527cfd6bacca1f7a401a513')

prepare() {
  cd "dosbox-x-dosbox-x-v$pkgver"
  autoreconf -fiv
}

build() {
  cd "dosbox-x-dosbox-x-v$pkgver"

  # FIXME: Workaround bug https://github.com/joncampbell123/dosbox-x/issues/3039
  export CFLAGS=${CFLAGS/-Werror=format-security/}
  export CXXFLAGS=${CXXFLAGS/-Werror=format-security/}

  ./configure \
    --prefix=/usr \
    --enable-sdl2 \
    --enable-xbrz \
    --enable-alsa-midi \
    --enable-avcodec
  make
}

package() {
  cd "dosbox-x-dosbox-x-v$pkgver"
  make DESTDIR="$pkgdir" install
}
# vim:set ts=2 sw=2 et:
