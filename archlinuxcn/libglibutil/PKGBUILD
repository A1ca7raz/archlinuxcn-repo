# Maintainer: Puqns67 <me@puqns67.icu>
# Contributor: Danct12 <danct12@disroot.org>

# This PKGBUILD is base on the AUR repo libglibutil, by Danct12 <danct12@disroot.org>

pkgname='libglibutil'
pkgver=1.0.76
pkgrel=1
pkgdesc='Library of glib utilities'
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='https://github.com/sailfishos/libglibutil'
license=('BSD-3-Clause')
depends=('glib2')
source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('f0b35f0366889b6a4c83d73e416a50022f2f0efc08f0fd34b6458c2e8c8b4cabeda6ebbcd3ce9682ab7b92640d956a534683aa2d6773771914ed5dbfb1ad99ee')

build() {
  KEEP_SYMBOLS=1 make -C "${srcdir}/${pkgname}-${pkgver}" release pkgconfig
}

package() {
  DESTDIR="${pkgdir}" make -C "${srcdir}/${pkgname}-${pkgver}" install-dev

  # license
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
