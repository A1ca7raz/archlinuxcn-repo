# Maintainer: Kat Witten <turtlewit@live.com>

pkgname=cglm
pkgver=0.9.2
pkgrel=1
pkgdesc="OpenGL Mathematics (glm) for C"
arch=('x86_64')
url="https://github.com/recp/${pkgname}"
license=('MIT')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('5c0639fe125c00ffaa73be5eeecd6be999839401e76cf4ee05ac2883447a5b4d')

build() {
	cd "${pkgname}-${pkgver}/"
	cmake -B build -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
	cmake --build build --config release
}

package() {
	cd "${pkgname}-${pkgver}/"
	make -C build DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm644 build/cglm.pc -t "${pkgdir}/usr/lib/pkgconfig/"
}
