# Maintainer: Roald Clark <roaldclark@gmail.com>
# Maintainer: Matthew Sexton <mssxtn@gmail.com>

pkgname=python-curl-cffi
_name=curl_cffi
pkgver=0.6.3b1
pkgrel=1
pkgdesc="Python binding for curl-impersonate via CFFI"
arch=('x86_64')
url="https://github.com/yifeikong/curl_cffi"
license=('MIT')
depends=('curl-impersonate-chrome' 'glibc' 'python' 'python-certifi' 'python-cffi' 'python-eventlet' 'python-gevent')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("${_name}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('226d7bb2e442474d15ef49cf8133bd623c64352d8f0fb49d8fb8ab5a62747291')

build() {
    cd "$_name-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "$_name-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
