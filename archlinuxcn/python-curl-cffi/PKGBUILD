# Maintainer: Roald Clark <roaldclark@gmail.com>
# Maintainer: Matthew Sexton <mssxtn@gmail.com>

pkgname=python-curl-cffi
_name=curl_cffi
pkgver=0.7.1
pkgrel=1
pkgdesc="Python binding for curl-impersonate via CFFI"
arch=('x86_64')
url="https://github.com/yifeikong/curl_cffi"
license=('MIT')
depends=(
    'curl-impersonate-chrome'
    'glibc'
    'python'
    'python-certifi'
    'python-cffi'
    'python-eventlet'
    'python-gevent'
)
makedepends=(
    'gcc13'
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
source=("${_name}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('d17ed473b20b5e5ab45a4e24b9cdf838977f644f06345afab3178842f5fd6992')

build() {
    cd "$_name-$pkgver"
    # Fix for GCC
    # https://github.com/nebulosa2007/PKGBUILDs/blob/main/archive/python-curl-cffi/PKGBUILD
    export CC=gcc-13
    export CXX=g++-13
    python -m build --wheel --no-isolation
}

package() {
    cd "$_name-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
