# Maintainer: Integral <integral@member.fsf.org>

pkgname=printsrcinfo-git
_pkgname=${pkgname%-git}
pkgver=0.9.6.r20.gdfbf093
pkgrel=1
pkgdesc="A naive PKGBUILD parsing library for Rust"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/7Ji/pkgbuild-rs"
license=('GPL-3.0-or-later')
makedepends=('git' 'cargo')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	(
		set -o pipefail
		git describe --long --abbrev=7 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
			printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
	)
}

prepare() {
	cd "${_pkgname}/"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${_pkgname}/"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --frozen --release --all-features --example printsrcinfo
}

package() {
	cd "${_pkgname}/"
	install -Dm755 "target/release/examples/${_pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
	install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${_pkgname}/"
}
