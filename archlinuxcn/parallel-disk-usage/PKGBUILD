# Maintainer: Integral <integral@member.fsf.org>
# Maintainer: Hoàng Văn Khải <hvksmr1996@gmail.com>

pkgname=parallel-disk-usage
_pkgname=pdu
pkgver=0.9.0
pkgrel=2
pkgdesc="Summarize disk usage of the set of files, recursively for directories."
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/KSXGitHub/${pkgname}"
license=('Apache')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('5aa3745204e205c399ac98e62dfc60b46fd568b6040fd330607bb442ee6491cc')

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --bin ${_pkgname} --release
}

package() {
	cd "${pkgname}-${pkgver}/"

	install -Dm755 "target/release/${_pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 "README.md" -t "${pkgdir}/usr/share/doc/${pkgname}/"
	install -Dm644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"

	install -Dm644 "exports/completion.bash" "${pkgdir}/usr/share/bash-completion/completions/${_pkgname}"
	install -Dm644 "exports/completion.fish" "${pkgdir}/usr/share/fish/completions/${_pkgname}.fish"
	install -Dm644 "exports/completion.zsh" "${pkgdir}/usr/share/zsh/site-functions/_${_pkgname}"
}
