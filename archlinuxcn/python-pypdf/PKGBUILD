# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=pypdf
pkgname=python-pypdf
pkgver=3.14.0
pkgrel=1
pkgdesc='A utility to read and write PDFs with Python (v3+)'
arch=('any')
url='https://github.com/py-pdf/pypdf'
license=('BSD')
depends=(
  python
)
optdepends=(
  python-pillow
  python-pycryptodome
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/py-pdf/pypdf/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('f7fc45a10249d3a86e60ed73f8e9a226a8bbb3fe181efdc8102128b7005a39f2479386801d18e8d6be56e8a12b5d3c8769a9c5504df27bec981ebe9cf22da205')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
