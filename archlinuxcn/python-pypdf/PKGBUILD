# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=pypdf
pkgname=python-pypdf
pkgver=3.15.1
pkgrel=1
pkgdesc='A utility to read and write PDFs with Python (v3+)'
arch=('any')
url='https://github.com/py-pdf/pypdf'
license=('BSD')
depends=(
  python
)
optdepends=(
  python-pillow
  python-pycryptodome
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/py-pdf/pypdf/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('93a4c5c573f8db73df4dd113e4d502bb183c2548a556686848dabf46078721e583c0283f6148c268d6e502e25402fab7b5e40d3eb9c60ab4db19827f183636e5')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
