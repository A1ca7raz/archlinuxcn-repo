# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=pypdf
pkgname=python-pypdf
pkgver=3.15.4
pkgrel=1
pkgdesc='A utility to read and write PDFs with Python (v3+)'
arch=('any')
url='https://github.com/py-pdf/pypdf'
license=('BSD')
depends=(
  python
)
optdepends=(
  python-pillow
  python-pycryptodome
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/py-pdf/pypdf/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('8d7db1c52700a2c9ec37c1756d04555708f4c859394c4866316d94470742a8d7900e5ec5d8fb2124ad285efc4848071d373347353226959b21dff84425b46513')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
