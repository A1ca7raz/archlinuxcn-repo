# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=python-crc32c
pkgname=python-google-crc32c
pkgver=1.5.0
pkgrel=2
pkgdesc='Python wrapping for google-crc32c'
arch=('x86_64')
url='https://github.com/googleapis/python-crc32c'
license=('Apache-2.0')
depends=(
  glibc
  google-crc32c
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/googleapis/python-crc32c/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('11b504e5f16b184d103acf2dcbc3ddbf9159951848240ea53577b06eb8daf71a9c872f0ddaeef55b18fcca902fcd16c912743fc0979f89792ec8e596bbe18fa5')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
