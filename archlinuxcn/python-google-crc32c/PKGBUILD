# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=python-crc32c
pkgname=python-google-crc32c
pkgver=1.6.0.dev1
pkgrel=1
pkgdesc='Python wrapping for google-crc32c'
arch=('x86_64')
url='https://github.com/googleapis/python-crc32c'
license=('Apache-2.0')
depends=(
  glibc
  google-crc32c
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/googleapis/python-crc32c/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('8267b8dd291cf3a33a0713a337ccb2166351c1e2866d6772e9ab7e5eed465e2f94ae362e2769321120e1d3e52315b97ed3ca147cc440bfc44093386a473e941c')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
