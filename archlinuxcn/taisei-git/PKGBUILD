# Maintainer: Lukas Weber <laochailan web de>
# Contributor: Andrei Alexeyev <akari taisei-project org>
# Contributor: cyberpunkrocker <l33tmmx gmail com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG

pkgname=taisei-git
_pkgname=${pkgname%-git}
pkgver=1.3.2
pkgrel=1
pkgdesc="Open source Touhou clone"
arch=('i686' 'x86_64')
url="https://taisei-project.org/"
license=('MIT')
depends=('opengl-driver' 'cglm' 'sdl2_mixer' 'freetype2' 'libpng' 'libwebp' 'libzip' 'hicolor-icon-theme')
optdepends=('spirv-cross: for OpenGL ES backends')
makedepends=('git' 'meson' 'python-docutils')
source=("git+https://github.com/taisei-project/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"
	arch-meson build
	ninja -C build
}

package() {
	cd "${_pkgname}/"
	DESTDIR="${pkgdir}" ninja install -C build
	install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
