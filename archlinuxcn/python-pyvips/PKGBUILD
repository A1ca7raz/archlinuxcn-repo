# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pyvips
_pkgname=pyvips
pkgver=2.2.2
pkgrel=1
pkgdesc='Python binding for libvips using cffi'
arch=(any)
url='https://github.com/libvips/pyvips'
license=(MIT)
depends=(
  libvips
  python-cffi
  python-numpy
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/libvips/pyvips/archive/refs/tags/v${pkgver}.tar.gz"
)
sha512sums=('d2a63f615ee135e8bf0481911776d28140a97a459c69d8b125ab61c4583d937a8208214b21a691d4aa0259287baf8a8922fd37168c8bed9d0ac33ccc0c28e777')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation -x
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 "LICENSE.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
