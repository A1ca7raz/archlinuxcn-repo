# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Frederick Zhang <frederick888@tsundere.moe>

pkgname=external-editor-revived
pkgver=1.0.0
pkgrel=1
pkgdesc="External Editor Revived is a Thunderbird MailExtension which allows editing emails in programs such as Vim, Neovim, Emacs, etc."
arch=('x86_64')
url="https://github.com/Frederick888/external-editor-revived"
license=('GPL3')
depends=('thunderbird>=91' 'gcc-libs' 'glibc')
makedepends=('cargo' 'zip')
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/Frederick888/external-editor-revived/archive/refs/tags/v$pkgver.tar.gz"
    "external_editor_revived.json"
)
b2sums=('c3431963f70604678ada51bbd746c7d935a32049d59e8c763d53afb5a2ee70ea68a4a57529c6a362171d003289f9d54f9dc8ce54cde68d38712cafc7b4b2f820'
        '5c3cda0e0cf5728fac3f025e4aa3a1b27838f4af2e9227b2e348a70ce6abeeef8da283f1f76a0e137d5c0b18a8e42ea834715af7f4a5f6abb8725a44c698f360')

prepare() {
    cd $pkgname-$pkgver
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$pkgname-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
    cd extension
    zip -r -FS "$srcdir/$pkgname@tsundere.moe.xpi" *
}

check() {
    cd $pkgname-$pkgver
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
    install -d "$pkgdir/usr/lib/thunderbird/extensions"
    install -Dm644 "$pkgname@tsundere.moe.xpi" "$pkgdir/usr/lib/thunderbird/extensions/$pkgname@tsundere.moe.xpi"

    install -d "$pkgdir/usr/lib/mozilla/native-messaging-hosts"
    install -Dm644 "external_editor_revived.json" "$pkgdir/usr/lib/mozilla/native-messaging-hosts/external_editor_revived.json"

    cd "$pkgname-$pkgver"
    install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"

    install -d "$pkgdir/usr/share/license/$pkgname"
    install -Dm644 "LICENSE" "$pkgdir/usr/share/license/$pkgname/LICENSE"
}
