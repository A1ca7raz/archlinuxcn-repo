# Maintainer: ThatOneCalculator <kainoa@t1c.dev>

pkgname="hyprpaper-git"
_pkgname="${pkgname%-git}"
pkgver=0.5.0.r8.gef0e051
pkgrel=1
pkgdesc="A blazing fast wayland wallpaper utility with IPC controls."
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/hyprwm/hyprpaper"
license=('BSD')
depends=(
	wayland
	libglvnd
	pango
	libjpeg
	libpng
	libwebp
	hyprlang-git
)
makedepends=(
	git
	cmake
	ninja
	meson
	wayland-protocols
	xorgproto
)
source=("${_pkgname}::git+https://github.com/hyprwm/hyprpaper.git")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
sha256sums=('SKIP')
options=(!makeflags !buildflags !strip)

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"
	make all
}

package() {
	cd "${_pkgname}/"
	install -Dm755 build/hyprpaper -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
