# Maintainer: ThatOneCalculator <kainoa@t1c.dev>

pkgname=hyprpaper-git
_pkgname="${pkgname%-git}"
pkgver=0.6.0.r8.g09c4062
pkgrel=1
pkgdesc="A blazing fast wayland wallpaper utility with IPC controls."
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/hyprwm/${_pkgname}"
license=('BSD-3-Clause')
depends=(
	wayland
	libglvnd
	pango
	libjpeg
	libpng
	libwebp
	hyprlang
)
makedepends=(
	git
	cmake
	ninja
	meson
	wayland-protocols
	xorgproto
)
source=("git+${url}.git")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
sha256sums=('SKIP')
options=(!makeflags !buildflags !strip)

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"
	make all
}

package() {
	cd "${_pkgname}/"
	install -Dm755 "build/${_pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
