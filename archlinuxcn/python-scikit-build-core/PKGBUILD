# Maintainer: Hu Butui <hot123tea123@gmail.com>

_name=scikit-build-core
pkgname=python-scikit-build-core
pkgdesc='A next generation Python CMake adaptor and Python API for plugins'
url='https://github.com/scikit-build/scikit-build-core'
pkgver=0.8.2
pkgrel=2
arch=('any')
license=('Apache-2.0')
depends=(
  cmake
  python-cattrs
  python-hatch-fancy-pypi-readme
  python-numpy
  python-packaging
  python-pathspec
  python-pyproject-metadata
  python-rich
  python-setuptools
  python-setuptools-scm
  python-tomli
)
makedepends=(
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
)
source=(
  "${_name}-${pkgver}.tar.gz::https://github.com/scikit-build/scikit-build-core/archive/refs/tags/v${pkgver}.tar.gz"
)
sha512sums=('76caf7d9cf072c1ee73f226b7ddbe8d839a8d66cbd0c5f50b0d1602a52f4b13c91528f15d24bc2593655e15a99a2602a898db1263e33d0dd4a7b185ad83291cc')

build() {
  cd "${_name}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_name}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
