# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=rchitect
pkgname=python-rchitect
pkgver=0.4.4
pkgrel=1
pkgdesc='Interoperate R with Python'
arch=('x86_64')
url='https://github.com/randy3k/rchitect'
license=('MIT')
depends=(
  python-cffi
  python-six
)
makedepends=(
  python-pip
  python-setuptools
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/randy3k/rchitect/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('3b16fb4349d674e2cc9b4bfa6e57a7e0c8fb18968c59915f30510a7f24a984f1cfee0c6788fd64e8c7caa67ce989d0e31c9b9b0c714767667ebd15947ce4e8c7')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
