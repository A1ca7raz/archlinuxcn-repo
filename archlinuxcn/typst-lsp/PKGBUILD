# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=typst-lsp
pkgver=0.13.0
pkgrel=1
pkgdesc="Language server for Typst"
arch=('i686' 'x86_64')
url="https://github.com/nvarner/${pkgname}"
license=('Apache-2.0 OR MIT')
depends=('openssl')
makedepends=('rust')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('860d56653b719402736b00ac9bc09e5e418dea2577cead30644252e85ab5d1a1')
options=('!lto')

prepare() {
	cd "${pkgname}-${pkgver}/"

	if [ ! -f "Cargo.lock" ]; then
		cargo update
	fi

	cargo fetch
}

package() {
	cd "${pkgname}-${pkgver}/"

	cargo install \
		--locked \
		--no-track \
		--root "${pkgdir}/usr" \
		--path .

	install -Dm644 LICENSE-{Apache-2.0,MIT}.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
