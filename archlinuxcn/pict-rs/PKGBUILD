# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pict-rs
_pkgver=0.4.5
pkgver="${_pkgver/-/.}"
pkgrel=1
pkgdesc="A simple image hosting service"
arch=('x86_64')
url="https://git.asonix.dog/asonix/pict-rs"
license=('AGPL3')
depends=('glibc' 'gcc-libs' 'imagemagick' 'ffmpeg' 'perl-image-exiftool')
makedepends=('rust' 'protobuf')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$_pkgver.tar.gz")
b2sums=('340bf6239cf1bfe157614b0da66b4996665a642fccc3884d68ea1418e462122570497e535f97c11c0073ae2ef8f34f85e9de8a005c6f60d6ef62c5c22668a505')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"
  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"
}
