# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pyreadr
_pkgname=pyreadr
pkgver=0.4.8
pkgrel=1
pkgdesc='Read and write R RData and Rds files into/from pandas dataframes'
arch=('x86_64')
url='https://github.com/ofajardo/pyreadr'
license=('AGPL3')
depends=(
  python-pandas
  xz
)
makedepends=(
  cython  
  python-setuptools
)
checkdepends=(
  python-sphinx_rtd_theme
  python-xarray
)
optdepends=(
  "python-xarray: for reading 3D arrays"
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/ofajardo/pyreadr/archive/refs/tags/v${pkgver}.tar.gz"
)
sha512sums=('2fbebb97be654ef102ac5c78a661bfa2a7f7cdc5500c55893ce2f7a4695fc6b33423e5e61e2cdf42b9ba8df5500b04bc3ff76f215112b16ba704ca387886e5b0')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  cd "${_pkgname}-${pkgver}"
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-cpython-${python_version}" python tests/test_basic.py
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:
