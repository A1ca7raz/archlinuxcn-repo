pkgbase=julia-git-hdf5-src
_subpkgs=(H5Zbitshuffle
          H5Zblosc
          H5Zbzip2
          H5Zlz4
          H5Zzstd)
pkgname=(julia-git-hdf5-src)
pkgver=0.17.2
_commit=c41a3d250306c3172cc0619e2d0e0704841082a7
pkgrel=1
pkgdesc="HDF5.jl"
url="https://github.com/JuliaIO/HDF5.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaIO/HDF5.jl.git#commit=$_commit")
sha256sums=('f589c00aa7b6a15e46ea6884054eb4f8bc933167d77219f01061585763757948')

package_julia-git-hdf5-src() {
  cd HDF5.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh HDF5 "${pkgdir}" "${pkgname}" julia-git

  rm -rf "${dest_dir}"/filters
}

_subpackage() {
  local _subpkg=$1
  cd HDF5.jl/filters/${_subpkg}

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh ${_subpkg} "${pkgdir}" "${pkgname}" julia-git
}

for _subpkg in "${_subpkgs[@]}"; do
  pkgname=("${pkgname[@]}" julia-git-${_subpkg,,}-src)
  eval "package_julia-git-${_subpkg,,}-src() { _subpackage ${_subpkg}; }"
done
