pkgbase=julia-git-gpuarrays-src
_subpkgs=(GPUArraysCore
          JLArrays)
pkgname=(julia-git-gpuarrays-src)
pkgver=10.2.0
_commit=800c237cc3d11b733f286d8070b91d41ebe65369
pkgrel=1
pkgdesc="GPUArrays.jl"
url="https://github.com/JuliaGPU/GPUArrays.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaGPU/GPUArrays.jl.git#commit=$_commit")
sha256sums=('ca80a10e981d2f2af6f756bf553300c0a4ec6a8de4320ae71a03424b775588cc')

package_julia-git-gpuarrays-src() {
  cd GPUArrays.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh GPUArrays "${pkgdir}" "${pkgname}" julia-git

  rm -rf "${dest_dir}"/lib
}

_subpackage() {
  local _subpkg=$1
  cd GPUArrays.jl/lib/${_subpkg}

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh ${_subpkg} "${pkgdir}" "${pkgname}" julia-git
}

for _subpkg in "${_subpkgs[@]}"; do
  pkgname=("${pkgname[@]}" julia-git-${_subpkg,,}-src)
  eval "package_julia-git-${_subpkg,,}-src() { _subpackage ${_subpkg}; }"
done
