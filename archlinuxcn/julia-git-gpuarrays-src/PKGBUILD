pkgbase=julia-git-gpuarrays-src
_subpkgs=(GPUArraysCore
          JLArrays)
pkgname=(julia-git-gpuarrays-src)
pkgver=10.1.1
_commit=353d2dfa8e21f03693dc24f4554c1cf462f80fc1
pkgrel=1
pkgdesc="GPUArrays.jl"
url="https://github.com/JuliaGPU/GPUArrays.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaGPU/GPUArrays.jl.git#commit=$_commit")
sha256sums=('15f3984d880670e2479e656e9255d00f131cc6b825814928b5162b9397b37030')

package_julia-git-gpuarrays-src() {
  cd GPUArrays.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh GPUArrays "${pkgdir}" "${pkgname}" julia-git

  rm -rf "${dest_dir}"/lib
}

_subpackage() {
  local _subpkg=$1
  cd GPUArrays.jl/lib/${_subpkg}

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh ${_subpkg} "${pkgdir}" "${pkgname}" julia-git
}

for _subpkg in "${_subpkgs[@]}"; do
  pkgname=("${pkgname[@]}" julia-git-${_subpkg,,}-src)
  eval "package_julia-git-${_subpkg,,}-src() { _subpackage ${_subpkg}; }"
done
