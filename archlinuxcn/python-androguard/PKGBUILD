_pkgname=androguard
pkgname="python-${_pkgname}"
pkgver=4.1.2
pkgrel=1
pkgdesc="Python framework for Android application analysis."
arch=("any")
url="https://github.com/${_pkgname}/${_pkgname}"
license=("Apache-2.0")
depends=("python-setuptools" "ipython" "python-future" "python-networkx" "python-pygments" "python-lxml" "python-colorama" "python-matplotlib" "python-asn1crypto" "python-click" "python-pydot")
makedepends=("python-build" "python-installer" "python-wheel" "python-poetry")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('fc76c74ce26d9fce1df252827063b7b36b3cf9c7df629df08268482153e99f72')

build() {
	cd "${_pkgname}-${pkgver}/"
	python -m build --wheel --no-isolation
}

package() {
	cd "${_pkgname}-${pkgver}/"
	python -m installer --destdir="${pkgdir}" dist/*.whl
}
