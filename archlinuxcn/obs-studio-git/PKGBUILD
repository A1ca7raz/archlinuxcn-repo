# Maintainer: Benjamin Klettbach <b dot klettbach at gmail dot com >
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: ArcticVanguard <LideEmily at gmail dot com>
# Contributor: ledti <antergist at gmail dot com>

pkgname=obs-studio-git
_pkgname=${pkgname%-git}
pkgver=30.1.0.rc1.r6.g4953c5d
pkgrel=1
pkgdesc="Free and open source software for video recording and live streaming."
arch=("i686" "x86_64")
url="https://github.com/obsproject/${_pkgname}"
license=("GPL-2.0-or-later")
depends=("ffmpeg" "jansson" "libxinerama" "libxkbcommon-x11" "mbedtls"
	"qt6-svg" "curl" "jack" "gtk-update-icon-cache"
	"speexdsp" "pciutils" "libajantv2" "librist" "onevpl"
	"libdatachannel")
makedepends=("asio" "cmake" "git" "libdatachannel" "libfdk-aac" "libxcomposite" "nlohmann-json" "onevpl" "websocketpp" "x264"
	"vlc" "swig" "luajit" "nlohmann-json" "python" "cef-minimal-obs-bin" "wayland"
	"websocketpp" "qt6-wayland" "pipewire" "xdg-desktop-portal" "qrcodegencpp-cmake")
optdepends=("libfdk-aac: FDK AAC codec support"
	"libxcomposite: XComposite capture support"
	"libva-intel-driver: hardware encoding"
	"libva-mesa-driver: hardware encoding"
	"vlc: VLC Media Source"
	"luajit: Lua scripting"
	"python: Python scripting"
	"v4l2loopback-dkms: Virtual webcam"
	"pipewire: Pipewire capture"
	"pipewire-media-session: Pipewire capture"
	"xdg-desktop-portal: Pipewire capture")
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+https://github.com/obsproject/${_pkgname}.git"
	"git+https://github.com/Mixer/ftl-sdk.git"
	"git+https://github.com/obsproject/obs-browser.git"
	"git+https://github.com/obsproject/obs-websocket.git"
	"git+https://github.com/nayuki/QR-Code-generator.git"
	"fix_python_binary_loading.patch")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'bdfbd062f080bc925588aec1989bb1df34bf779cc2fc08ac27236679cf612abd')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "${_pkgname}/"
	gitconf="protocol.file.allow=always"

	patch -Np1 <"${srcdir}/fix_python_binary_loading.patch"
	git config "submodule.plugins/obs-outputs/ftl-sdk.url" "${srcdir}/ftl-sdk"
	git config "submodule.plugins/obs-browser.url" "${srcdir}/obs-browser"
	git config "submodule.plugins/obs-websocket.url" "${srcdir}/obs-websocket"
	git -c ${gitconf} submodule update

	cd "plugins/obs-websocket/"
	git config "submodule.deps/qr.url" "${srcdir}/QR-Code-generator"
	git -c ${gitconf} submodule update
}

build() {
	cd "${_pkgname}/"
	mkdir -p build && cd build

	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DBUILD_BROWSER=ON \
		-DCEF_ROOT_DIR="/opt/cef-obs" \
		-DCALM_DEPRECATION=ON \
		..

	make
}

package() {
	cd "${_pkgname}/build/"
	make install DESTDIR="${pkgdir}"
}

# vim: ts=2:sw=2:expandtab
