# Maintainer: American_Jesus <american.jesus.pt AT gmail DOT com>

_pkgname=nano-syntax-highlighting
pkgname=$_pkgname-git
pkgver=621.1aa64a8
pkgrel=3
pkgdesc="Nano editor syntax highlighting enhancements"
arch=('any')
depends=('nano')
makedepends=('git')
url="https://github.com/scopatz/nanorc"
license=('GPL-3.0-or-later')
install=nano-syntax-highlighting.install
provides=('nano-syntax-highlighting')
conflicts=('nano-syntax-highlighting')
source=('nanorc::git+https://github.com/scopatz/nanorc.git#branch=master'
		'0001-fix-normal-color-takes-no-prefix.patch')
sha256sums=('SKIP'
            '3c65854506a46e33ebc94842f4338351374a7ac18cc85f837da2e594839fa94f')

pkgver() {
	cd $srcdir/nanorc
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
	cd "${srcdir}/nanorc"
	patch -Np1 -i "${srcdir}/0001-fix-normal-color-takes-no-prefix.patch"
}

build() {
	# Generate nanorc.sample
	find" ${srcdir}/nanorc/*.nanorc" -type f | sed "s@${srcdir}\/nanorc@include \/usr\/share\/${_pkgname}@" > "${srcdir}/nanorc.sample"
}

package() {
	cd "${srcdir}"
	mkdir -p "${pkgdir}/usr/share/${_pkgname}"
	find "${srcdir}/nanorc/" -name '*.nanorc' | xargs install -D -m644 -t "${pkgdir}/usr/share/${_pkgname}/"
	install -D -m644 "$srcdir/nanorc.sample" "${pkgdir}/usr/share/${_pkgname}/nanorc.sample"
} 


