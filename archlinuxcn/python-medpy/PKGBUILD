# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-medpy
_pkgname=medpy
pkgver=0.5.0rc1
pkgrel=2
pkgdesc='Medical image processing in Python'
arch=('x86_64')
url='https://github.com/loli/medpy'
license=('GPL-3.0-or-later')
depends=(
  boost-libs
  gcc-libs
  glibc
  python-numpy
  python-scipy
  python-simpleitk
)
makedepends=(
  boost
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/loli/medpy/archive/${pkgver}.tar.gz")
sha512sums=('5d02077a9b2d8b1cb45d5d13461ec6b8c88bc0f50fcf0269ee599f09e8985845cb315821a8a2599fa2cb15b8a84c559576e9ab0833d3f04f39d4a6c467a56a1b')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
