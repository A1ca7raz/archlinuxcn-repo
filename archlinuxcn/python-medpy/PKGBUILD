# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-medpy
_pkgname=medpy
pkgver=0.5.0rc2
pkgrel=1
pkgdesc='Medical image processing in Python'
arch=('x86_64')
url='https://github.com/loli/medpy'
license=('GPL-3.0-or-later')
depends=(
  boost-libs
  gcc-libs
  glibc
  python-numpy
  python-scipy
  python-simpleitk
)
makedepends=(
  boost
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/loli/medpy/archive/${pkgver}.tar.gz")
sha512sums=('5e81e94c04f801bfcf883681b9a8a5d8072595fa4be4e10aea054bad734ea9172a0486b608fe0bbe2557f1d056b235c82531d939d630bb2ca5c5a98e01a86ed0')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
