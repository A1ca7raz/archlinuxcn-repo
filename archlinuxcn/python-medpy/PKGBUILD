# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-medpy
_pkgname=medpy
pkgver=0.5.1
pkgrel=2
pkgdesc='Medical image processing in Python'
arch=('x86_64')
url='https://github.com/loli/medpy'
license=('GPL-3.0-or-later')
depends=(
  boost-libs
  gcc-libs
  glibc
  python-numpy
  python-scipy
  python-simpleitk
)
makedepends=(
  boost
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/loli/medpy/archive/${pkgver}.tar.gz")
sha512sums=('5e6f92c47fe4cfbc5df07fa47c9a61649d36f82d701ef66eba2649649e2880783f3eed59b655df4d2d3f0be53c8234b95da5bfe937ac596f51305b8507e683dc')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
