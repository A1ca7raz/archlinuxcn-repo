# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=plotly.py
pkgname=python-plotly
pkgver=5.21.0
pkgrel=2
pkgdesc='The interactive graphing library for Python (includes Plotly Express)'
url='https://plotly.com/python'
depends=(
  python-matplotlib
  python-numpy
  python-pandas
  python-six
  python-tenacity
)
optdepends=(
  'ipython: ipython support'
  'python-jupyter: jupyter support'
  'python-ipykernel: jupyter support'
  'python-pillow: image uri conversion'
)
makedepends=(
  jupyter-notebook
  jupyterlab
  python-build
  python-installer
  python-setuptools
  python-wheel
)
license=('MIT')
arch=('any')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/plotly/plotly.py/archive/v${pkgver}.tar.gz")
sha512sums=('38841fae844452550004fd22126c12c19fa65dae8985e8449eb5ff5be53dcb04a20658afc13a675c6086db2c623e0902e4138cd28f3b4c9d0a9a0013c1465c13')

build() {
  cd ${_pkgname}-${pkgver}/packages/python/plotly
  SKIP_NPM=1 python -m build --wheel --no-isolation -x
}

package() {
  cd ${_pkgname}-${pkgver}/packages/python/plotly
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  mv -vf "${pkgdir}/usr/etc" "${pkgdir}"
}
# vim:set ts=2 sw=2 et:
