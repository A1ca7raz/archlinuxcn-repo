# Maintainer: seth <getchoo at tuta dot io>

pkgname=joshuto
pkgver=0.9.5
pkgrel=2
pkgdesc="ranger-like terminal file manager written in Rust"
arch=('x86_64' 'aarch64')
url="https://github.com/kamiyaa/${pkgname}"
license=('LGPL3')
makedepends=('cargo')
optdepends=(
	'fzf: for better file searching'
	'xclip: for clipboard support on X11'
	'xsel: for clipboard support on X11'
	'wl-clipboard: for clipboard support on Wayland'
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('14155a02cfeb0993a4906635a3c121589451e155e067d1c0d1673abdd1494ca8')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "${pkgname}-${pkgver}/"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --frozen --release --all-features
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
