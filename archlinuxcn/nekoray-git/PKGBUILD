# Maintainer: Chi_Tang <me@chitang.dev>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=nekoray-git
_pkgname=${pkgname%-git}
pkgver=4.0.beta1.r1.gdf141f9
pkgrel=3
pkgdesc="Qt based cross-platform GUI proxy configuration manager (backend: sing-box)"
arch=('x86_64')
url="https://matsuridayo.github.io"
license=('GPL-3.0-or-later')
makedepends=('cmake' 'git' 'ninja' 'go')
depends=(
	'qt6-base' 'qt6-svg' 'qt6-tools'
	'protobuf' 'yaml-cpp' 'zxing-cpp'
)
optdepends=(
	'sing-geoip: geoip data for NekoBox'
	'sing-geosite: geosite data for NekoBox'
)
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=(
	"git+https://github.com/MatsuriDayo/${_pkgname}.git"
	"${_pkgname}.sh"
	"${_pkgname}.desktop"
)
sha512sums=('SKIP'
            'b377f7e8c859ee0b5bc05f89e2dc6c0b8535e740e089e9afe5e5f145c38a05fccbbddfcb6eb8ced9a7478e4d9ccc3cbd3ffb6843c128189c75599c40c87737e8'
            'c8ad5a0cf5d813e1e35b827f92d526c022d529d34dd4019066b550c8f0dbdb89b0fb62e6bfc650e8c18acac826591e858eff3b2e5e6c6f5014f0f4b50f1fa88b')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "${_pkgname}/"
	git submodule init
	git submodule update
	bash libs/get_source.sh

	mkdir build
	cd build
	cmake -GNinja -DQT_VERSION_MAJOR=6 -DNKR_PACKAGE=ON ..
	ninja

	cd ..
	GOOS=linux GOARCH=amd64 ./libs/build_go.sh
}

package() {
	install -dm755 "${pkgdir}/usr/bin/"

	# Assets
	install -Dm644 "${_pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"
	install -Dm644 ${_pkgname}/res/public/{nekoray,nekobox}.png -t "${pkgdir}/usr/share/pixmaps/"

	# Core
	install -Dm755 "${_pkgname}/deployment/linux64/nekobox_core" -t "${pkgdir}/usr/lib/${_pkgname}/"

	# Binary file
	install -Dm755 "${_pkgname}/build/${_pkgname}" -t "${pkgdir}/usr/lib/${_pkgname}/"

	# Launcher
	install -Dm755 "${_pkgname}.sh" "${pkgdir}/usr/bin/${_pkgname}"
}
