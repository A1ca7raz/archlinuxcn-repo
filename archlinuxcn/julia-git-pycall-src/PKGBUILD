pkgname=julia-git-pycall-src
pkgver=1.96.1
_commit=2a9f07725b1bce8c07ac5437d66701dd9579a221
pkgrel=3
pkgdesc="PyCall.jl"
url="https://github.com/JuliaPy/PyCall.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts julia-git-conda-src julia-git-json-src
             julia-git-parsers-src julia-git-versionparsing-src)
depends=(julia-git python)
source=("git+https://github.com/JuliaPy/PyCall.jl.git#commit=$_commit"
        0001-Fix-breakage-due-to-JuliaLang-julia-51319.patch)
sha256sums=('SKIP'
            'a94af190c371c7f0e5d71b11b418711ca6f898ec196117d74d2d84c965aa69a7')

prepare() {
  cd PyCall.jl

  patch -Np1 --no-backup-if-mismatch < "../0001-Fix-breakage-due-to-JuliaLang-julia-51319.patch"
}

build() {
  cd PyCall.jl/deps

  JULIA_DEPOT_PATH="${srcdir}/.julia" julia --startup-file=no build.jl
}

package() {
  cd PyCall.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh PyCall "${pkgdir}" "${pkgname}" julia-git

  pymin_ver=$(python -c 'import sys; print(sys.version_info.minor)')
  ((pynext_min=pymin_ver + 1))
  depends+=("python>=3.${pymin_ver}.0" "python<3.${pynext_min}.0")
}
