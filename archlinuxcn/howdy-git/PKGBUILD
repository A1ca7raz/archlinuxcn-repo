# Maintainer: Caleb Fontenot <foley2431@gmail.com>

pkgname=howdy-git
_pkgname="${pkgname%-git}"
pkgver=2.6.1.r77.g96767fe
pkgrel=1
pkgdesc="Windows Hello for Linux"
arch=('x86_64')
url="https://github.com/boltgolt/${_pkgname}"
license=('MIT')
depends=(
	'opencv'
	'hdf5'
	'pam-python'
	'python'
	'python-pillow'
	'python-dlib'
	'python-face_recognition'
	'python-face_recognition_models'
	'python-click'
	'python-numpy'
	'python-opencv'
)
makedepends=(
	'git'
	'cmake'
	'pkgfile'
)
conflicts=("${_pkgname}")
provides=("${_pkgname}")
backup=('usr/lib/security/${_pkgname}/config.ini')
source=(
	"git+${url}.git"
	"https://github.com/davisking/dlib-models/raw/master/dlib_face_recognition_resnet_model_v1.dat.bz2"
	"https://github.com/davisking/dlib-models/raw/master/mmod_human_face_detector.dat.bz2"
	"https://github.com/davisking/dlib-models/raw/master/shape_predictor_5_face_landmarks.dat.bz2"
)
sha256sums=('SKIP'
	'abb1f61041e434465855ce81c2bd546e830d28bcbed8d27ffbe5bb408b11553a'
	'db9e9e40f092c118d5eb3e643935b216838170793559515541c56a2b50d9fc84'
	'6e787bbebf5c9efdb793f6cd1f023230c4413306605f24f299f12869f95aa472')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	cd "${_pkgname}/"

	# Install the license files and the rest of howdy
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}"

	install -d "${pkgdir}/usr/lib/security/${_pkgname}/"
	cp -rv ${_pkgname}/src/* "${pkgdir}/usr/lib/security/${_pkgname}"

	install -d "${pkgdir}/usr/lib/${_pkgname}-gtk"
	cp -rv ${_pkgname}-gtk/src/* "${pkgdir}/usr/lib/howdy-gtk"

	pushd "${pkgdir}/usr/lib/security/${_pkgname}/dlib-data/"
	install -Dm644 "${srcdir}/dlib_face_recognition_resnet_model_v1.dat" -t .
	install -Dm644 "${srcdir}/mmod_human_face_detector.dat" -t .
	install -Dm644 "${srcdir}/shape_predictor_5_face_landmarks.dat" -t .
	popd

	install -d "${pkgdir}/usr/bin/"
	ln -s "/lib/security/${_pkgname}/cli.py" "${pkgdir}/usr/bin/${_pkgname}"
	chmod +x "${pkgdir}/usr/lib/security/${_pkgname}/cli.py"

	install -Dm755 "${_pkgname}/src/autocomplete/${_pkgname}" -t "${pkgdir}/usr/share/bash-completion/completions/"
}
