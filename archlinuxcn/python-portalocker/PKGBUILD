# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Maintainer: Ashley Bone <ashley DOT bone AT pm DOT me>

pkgname=python-portalocker
_pkgname=portalocker
pkgver=2.7.0
pkgrel=2
pkgdesc='An extended version of portalocker to lock files in Python using the with statement'
arch=('any')
url='https://github.com/WoLpH/portalocker'
license=('PSF')
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/wolph/portalocker/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('4182089b5b5ad8a09ea863b5b8103e2cf9f1e793691f2e43fb53ec2d3f397df4')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
