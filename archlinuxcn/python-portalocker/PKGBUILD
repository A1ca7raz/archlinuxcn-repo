# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Maintainer: Ashley Bone <ashley DOT bone AT pm DOT me>

pkgname=python-portalocker
_pkgname=portalocker
pkgver=2.10.0
pkgrel=1
pkgdesc='An extended version of portalocker to lock files in Python using the with statement'
arch=('any')
url='https://github.com/WoLpH/portalocker'
license=('BSD-3-Clause')
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/wolph/portalocker/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('d4d4418bf730bc19d0a4b0a0201cb3b004c59b1e1770fa3f5c4a139e0d524802')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
