# Maintainer: tohmais <callum.wishart@protonmail.com>

_pkgname="hyprland"
pkgname="${_pkgname}-nvidia"
pkgver=0.32.3
pkgrel=1
pkgdesc="A dynamic tiling Wayland compositor based on wlroots that doesn't sacrifice on its looks. (NVIDIA patch)"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/hyprwm/Hyprland"
license=('BSD')
depends=(
	libxcb
	xcb-proto
	xcb-util
	xcb-util-keysyms
	libxfixes
	libx11
	libxcomposite
	xorg-xinput
	libxrender
	pixman
	wayland-protocols
	cairo
	pango
	polkit
	glslang
	libinput
	libxcb
	libxkbcommon
	opengl-driver
	pixman
	wayland
	xcb-util-errors
	xcb-util-renderutil
	xcb-util-wm
	seatd
	vulkan-icd-loader
	vulkan-validation-layers
	xorg-xwayland
	libliftoff
	libdisplay-info)
makedepends=(
	git
	cmake
	ninja
	gcc
	gdb
	meson
	vulkan-headers
	wayland-protocols
	xorgproto
	jq)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hyprwm/Hyprland/releases/download/v${pkgver}/source-v${pkgver}.tar.gz"
	"nvidia.patch")
conflicts=("${_pkgname}")
provides=(hyprland)
sha256sums=('285e8ebbd003978a2124e984db7677d044c21ca3cb72948eb71742cd8396df35'
            '10223a97a622ea7e641b4305a9a4f7bf0cb0526dacfbfa5970171558b3a63488')

prepare() {
	cd hyprland-source/subprojects/wlroots/
	patch -p1 <../packagefiles/wlroots-meson-build.patch
	patch --directory=. --forward --strip=0 --input="${srcdir}/nvidia.patch"
}

build() {
	cd hyprland-source/

	meson setup build \
		--prefix /usr \
		--libexecdir lib \
		--sbindir bin \
		--buildtype plain \
		--wrap-mode nodownload \
		-D b_lto=true \
		-D b_pie=true \
		-D default_library=shared \
		-D xwayland=enabled

	ln -sf wlroots build/subprojects/wlroots/include/wlr
	meson compile -C build
}

package() {
	cd hyprland-source/
	meson install -C build --destdir "${pkgdir}"

	rm -rf "${pkgdir}/usr/include/hyprland/wlroots/wlr/"
	ln -sf . "${pkgdir}/usr/include/hyprland/wlroots/wlr"

	# resolve conflicts with system wlr
	rm -f "${pkgdir}/usr/lib/libwlroots.so"
	rm -f "${pkgdir}/usr/lib/pkgconfig/wlroots.pc"

	# resolve conflicts with xdg-desktop-portal-hyprland from repo
	rm -rf "${pkgdir}/usr/share/xdg-desktop-portal/"
	rm -rf "${pkgdir}/usr/share/hyprland-protocols/"
	rm -rf "${pkgdir}/usr/share/pkgconfig/hyprland-protocols.pc"

	# FIXME: meson.build shall install version.h
	install -Dm644 src/version.h -t "${pkgdir}/usr/include/hyprland/src/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
