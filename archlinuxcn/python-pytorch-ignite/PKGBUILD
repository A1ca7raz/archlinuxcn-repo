# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pytorch-ignite
_pkgname=ignite
pkgver=0.4.13
pkgrel=1
pkgdesc='High-level library to help with training and evaluating neural networks in PyTorch flexibly and transparently'
arch=('any')
url='https://github.com/pytorch/ignite'
license=('BSD')
depends=(
  python-pytorch
)
makedepends=(
  python-setuptools
)

source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pytorch/ignite/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('f370237057a901129816f8b311e98cfd622163424815c55d31af282ef856c362243de036c11343a47d384f9b8ef23d05b7cfe2470b27111ab15d0f4c660264fe')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
