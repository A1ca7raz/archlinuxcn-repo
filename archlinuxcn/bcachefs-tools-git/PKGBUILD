# Maintainer: Kyle De'Vir (QuartzDragon) <kyle.devir.mykolab.com>

pkgname=bcachefs-tools-git
_pkgname=${pkgname%-git}
pkgver=r1261.f5baaf4
pkgrel=1
epoch=1
pkgdesc="BCacheFS filesystem utilities"
url="https://github.com/koverstreet/${_pkgname}"
arch=("x86_64")
license=("GPL2")
install="${pkgname}.install"
provides=("${_pkgname}")
_dependsarray="attr cargo clang fuse3 git keyutils libaio libscrypt libsodium liburcu libutil-linux pkgconf python-docutils systemd valgrind zlib"
makedepends=(${_dependsarray})
depends=(${_dependsarray})
options=('!strip')
source=(
	"git+${url}.git"
	"add-mkinitcpio-hook-for-Arch.patch"
)
sha512sums=('SKIP'
            'e75e0fc9576990d910acf5a3eafb1f83ab7d407bddcc210c9926b048ceb3ef018749654ca6a6ebdfab45a3e00031f6bb3537d2f3f19e3a7ec3be7c470d4ce7df')

prepare() {
	cd "${_pkgname}/"

	PName="add-mkinitcpio-hook-for-Arch.patch"
	msg2 "Patching with $PName ..."
	patch -Np1 -i "../$PName"
}

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"
	make
}

package() {
	cd "${_pkgname}/"

	make DESTDIR="${pkgdir}" PREFIX="/usr" ROOT_SBINDIR="/usr/bin" INITRAMFS_DIR="/etc/initcpio" install

	install -Dm644 "arch/etc/initcpio/hooks/bcachefs" -t "${pkgdir}/etc/initcpio/hooks/"
	install -Dm644 "arch/etc/initcpio/install/bcachefs" -t "${pkgdir}/etc/initcpio/install/"
}
