# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: silverhikari <kerrickethan@gmail.com>
# Contributor: Wouter Wijsman <wwijsman@live.nl>

pkgname=dosbox-staging
pkgver=0.81.1
pkgrel=1
pkgdesc="DOS/x86 emulator focusing on ease of use. Based on DOSBox"
arch=('x86_64')
url="https://github.com/dosbox-staging/dosbox-staging"
license=('GPL-2.0-or-later')
depends=('sdl2' 'sdl2_net' 'sdl2_image' 'opusfile'  'alsa-lib' 'fluidsynth' 'libpng' 'munt' 'libslirp' 'glib2' 'libxi')
optdepends=(
            'ncurses'
            )
makedepends=('meson' 'ninja' 'gcc' 'gzip' 'gtest')
provides=("dosbox")
conflicts=("dosbox")
options=(debug)
source=(
  "${pkgname}-${pkgver}.gz::https://github.com/dosbox-staging/${pkgname}/archive/v${pkgver}.tar.gz"
)
b2sums=('dfee4255db923aba1be2941e0c70fbd0bccc894a245c68907b25d111209cdac3c8cf7854427197915f55a37bdf32b3450e7afa048ee4200ea36de11facfefbae')

prepare() {
  cd "$srcdir/${pkgname}-${pkgver}"
}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  arch-meson . build \
    --wrap-mode default \
    --buildtype=debugoptimized \
    -Ddefault_library=shared \
    -Dtry_static_libs=iir
  meson compile -C build
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  
  meson install -C build --no-rebuild --skip-subprojects --destdir="$pkgdir"

  # dosbox-staging documents
  install -Dm 644 "docs/README.video" "$pkgdir/usr/share/doc/${pkgname}/video.txt"
  install -Dm 644 "README" "$pkgdir/usr/share/doc/${pkgname}/manual.txt"

  # bash completions
  install -Dm 644 "contrib/linux/bash-completion/dosbox" "$pkgdir/usr/share/bash-completion/completions/dosbox"
}
# vim: set sts=2 sw=2:
