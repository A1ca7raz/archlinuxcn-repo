# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor:Oleksandr Natalenko <oleksandr@natalenko.name>

pkgname=uksmd
pkgver=6.5.1
pkgrel=2
pkgdesc="Userspace KSM helper daemon"
url=https://codeberg.org/pf-kernel/uksmd
license=(GPL-3.0-only)
arch=(x86_64)
depends=(systemd-libs glibc procps-ng libcap-ng UKSMD-BUILTIN)
optdepends=('uksmdstats: for parsing /sys KSM statistics')
makedepends=(meson)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
b2sums=('12c0ed088ce189470a63ed74de02b35c994363500a04a28e2b1a8e7ad1ad3b75970cf69af939476b00b8bf1a9838591f83051dc004f2f91d74f19fd17746c37f')

build() {
	cd $pkgname
	arch-meson . build
	meson compile -C build
}

package() {
	cd $pkgname
	meson install -C build --destdir "$pkgdir"
}
