# Maintainer: Ikosse <andrelofgren@hotmail.co.uk>

pkgname=harbour-amazfish
pkgver=2.2.8
pkgrel=1
pkgdesc="Companion application for Huami Devices and the Pinetime Infinitime"
arch=('x86_64' 'aarch64')
url="https://github.com/piggz/${pkgname}"
provides=('amazfish')
license=('GPL-3.0-or-later')
depends=(
	'qt5-base'
	'karchive5'
	'kdb'
	'kcontacts5'
	'qt5-location'
	'qt5-connectivity'
	'qtmpris'
	'nemo-qml-plugin-dbus'
	'kirigami2'
	'bluez-qt5'
)
makedepends=('git')
source=("${pkgname}-${pkgver}::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('8acc62d7db8726ca3c47db28bf20f707c270f8c640f313b56ec94852441dbbc0cf524a649d93f971755c47f3b83101111a905d09d11bc0ce9e84d103668a1d2a')

prepare() {
	cd "${pkgname}-${pkgver}/"
	rm -rf qble daemon/libwatchfish
	git init
	git submodule add https://github.com/piggz/qble.git qble
	git submodule add https://github.com/piggz/libwatchfish.git daemon/libwatchfish
}

build() {
	cd "${pkgname}-${pkgver}/"
	qmake FLAVOR=kirigami PREFIX=/usr .
	make
}

package() {
	cd "${pkgname}-${pkgver}/"
	make INSTALL_ROOT="${pkgdir}" install
}
