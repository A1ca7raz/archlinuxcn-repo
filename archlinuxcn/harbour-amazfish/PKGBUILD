# Maintainer: Ikosse <andrelofgren@hotmail.co.uk>

pkgname=harbour-amazfish
pkgver=2.2.2
pkgrel=1
pkgdesc="Companion application for Huami Devices and the Pinetime Infinitime"
arch=('x86_64' 'aarch64')
url="https://github.com/piggz/${pkgname}"
provides=('amazfish')
license=('GPL-3.0-or-later')
depends=(
	'qt5-base'
	'karchive5'
	'kdb'
	'kcontacts5'
	'qt5-location'
	'qt5-connectivity'
	'qtmpris'
	'nemo-qml-plugin-dbus'
	'kirigami2'
	'bluez-qt5'
)
makedepends=('git')
source=("${pkgname}-${pkgver}::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('2b12c77496c3c0319f6bb4dbac940cca5eab49052c247549e65ad6e85170830d35058e10d7046ec1b112b36b0bf66ca7abbd891e42f273303d6b8a344c8055d5')

prepare() {
	cd "${pkgname}-${pkgver}/"
	rm -rf qble daemon/libwatchfish
	git init
	git submodule add https://github.com/piggz/qble.git qble
	git submodule add https://github.com/piggz/libwatchfish.git daemon/libwatchfish
}

build() {
	cd "${pkgname}-${pkgver}/"
	qmake FLAVOR=kirigami PREFIX=/usr .
	make
}

package() {
	cd "${pkgname}-${pkgver}/"
	make INSTALL_ROOT="${pkgdir}" install
}
