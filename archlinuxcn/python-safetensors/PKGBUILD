# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=safetensors
pkgname=python-safetensors
pkgver=0.3.3.rc1
pkgrel=1
pkgdesc='Simple, safe way to store and distribute tensors'
arch=('x86_64')
url='https://github.com/huggingface/safetensors'
license=('Apache')
depends=(
  python-pymongo
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-rust
  python-wheel
)
optdepends=(
  python-numpy
  python-pytorch
  python-tensorflow
  python-huggingface-hub
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/huggingface/safetensors/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('e8d3615cf2f56bc73abc67032eb6ff9ea9697637ca3fdb78db769109264dcecc595362a542757e564db45557e17b60d4d3c028481d329471bbfadbfae1856ce2')

build() {
  cd "${_pkgname}-${pkgver}/bindings/python"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}/bindings/python"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
