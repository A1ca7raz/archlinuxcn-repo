# Maintainer : Antoine Viallon <antoine+aur@lesviallon.fr>

pkgname=video2x
pkgver=4.8.1
pkgrel=2
pkgdesc="Machine learning video/GIF/image upscaling"
url="https://${pkgname}.org/"
arch=("any")
license=("GPL-3.0-or-later")
depends=(
	"python"
	"ffmpeg"
	"python-pyaml"
	"python-avalon_framework" # AUR
	"python-colorama"
	"patool" # AUR, should be named python-patool
	"python-pillow"
	#"python-pyqt5"
	"python-requests"
	"python-tqdm"
	"python-magic"
)
optdepends=(
	"waifu2x-ncnn-vulkan: for anime/cartoon upscaling and JPEG denoising - fast"
	"realsr-ncnn-vulkan: real photos upscaling and denoising - slow"
	"srmd-ncnn-vulkan: general purpose upscaling and image restoration"
	"waifu2x-converter-cpp: C++ implemenation of waifu2x working on all platforms"
)
source=(
	"${pkgname}-${pkgver}::https://github.com/k4yt3x/${pkgname}/archive/${pkgver}.tar.gz"
	"etc_config_file.patch"
	"${pkgname}.yaml"
)
sha256sums=('a372027fb09c45f6624db8839b46fa34b4c3c639ba19fe9d192f9ef0a4ed9c46'
            '9d027d3f8ca5bdf4e7c543fe5cd18e5024cf7ce0e74c2d52db0a3d1ca27fcc5e'
            'dae9fb91b965d27a868ab71e36c8d6633f0c4be770c8f70c2ea41bc957b7c796')

prepare() {
	cd "${pkgname}-${pkgver}/src/"
	patch "${pkgname}.py" "${srcdir}/etc_config_file.patch"
}

package() {
	cd "${pkgname}-${pkgver}/src/"

	install -d "${pkgdir}/usr/share/${pkgname}/wrappers/"
	install -d "${pkgdir}/usr/bin/"

	for file in image_cleaner.py upscaler.py "${pkgname}.py" exceptions.py bilogger.py progress_monitor.py; do
		install -Dm755 "${file}" -t "${pkgdir}/usr/share/${pkgname}/"
	done

	for file in wrappers/*.py; do
		install -Dm755 "${file}" -t "${pkgdir}/usr/share/${pkgname}/wrappers/"
	done

	install -Dm644 "${srcdir}/${pkgname}.yaml" -t "${pkgdir}/etc/"
	ln -s "/usr/share/${pkgname}/${pkgname}.py" "${pkgdir}/usr/bin/${pkgname}"
}
