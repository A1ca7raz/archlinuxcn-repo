# Maintainer : Antoine Viallon <antoine+aur@lesviallon.fr>

pkgname=video2x
pkgver=5.0.0.beta6
_pkgver=${pkgver/.beta/-beta}
pkgrel=1
pkgdesc="Machine learning video/GIF/image upscaling"
url="https://${pkgname}.org/"
arch=("any")
license=("GPL-3.0-or-later")
depends=(
	"python"
	"ffmpeg"
	"python-pyaml"
	"python-avalon_framework" # AUR
	"python-colorama"
	"patool" # AUR, should be named python-patool
	"python-pillow"
	#"python-pyqt5"
	"python-requests"
	"python-tqdm"
	"python-magic"
)
optdepends=(
	"waifu2x-ncnn-vulkan: for anime/cartoon upscaling and JPEG denoising - fast"
	"realsr-ncnn-vulkan: real photos upscaling and denoising - slow"
	"srmd-ncnn-vulkan: general purpose upscaling and image restoration"
	"waifu2x-converter-cpp: C++ implemenation of waifu2x working on all platforms"
)
source=(
	"${pkgname}-${_pkgver}.tar.gz::https://github.com/k4yt3x/${pkgname}/archive/${_pkgver}.tar.gz"
	"${pkgname}.yaml"
)
sha256sums=('6fd01242986265dfedd59cfa589c0649e1036d4bcc959af4427067acc3e23d8c'
            'dae9fb91b965d27a868ab71e36c8d6633f0c4be770c8f70c2ea41bc957b7c796')

package() {
	cd "${pkgname}-${_pkgver}/"

	install -d "${pkgdir}/usr/share/${pkgname}/wrappers/"
	install -d "${pkgdir}/usr/bin/"

	for file in video2x/*.py; do
		install -Dm755 "${file}" -t "${pkgdir}/usr/share/${pkgname}/"
	done

	install -Dm644 "${srcdir}/${pkgname}.yaml" -t "${pkgdir}/etc/"
	ln -s "/usr/share/${pkgname}/${pkgname}.py" "${pkgdir}/usr/bin/${pkgname}"
	install -d "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm644 licenses/* "${pkgdir}/usr/share/licenses/${pkgname}/"
}
