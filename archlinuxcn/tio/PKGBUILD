# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Martin Hundeb√∏ll <martin@hundeboll.net>

pkgname=tio
pkgver=3.5
pkgrel=1
pkgdesc="The simple TTY terminal I/O application"
url="https://github.com/tio/tio"
arch=('x86_64' 'i686' 'arm' 'armv7h' 'armv6h' 'aarch64')
license=('GPL-2.0-or-later')
depends=(
  'glib2'
  'glibc'
  'lua'
)
makedepends=(
  'bash-completion'
  'meson'
)
source=("https://github.com/tio/tio/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz")
b2sums=('47423adff5be1cb15fcb0312ded179f3e1b3e872d927f5c81ad623ef708f593ff445617605b531360e5bc3310c66f43b6f45c3b05bba825cf8a9e85642cd7fd5')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

package() {
  depends+=('libglib-2.0.so')
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
