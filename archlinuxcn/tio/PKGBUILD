# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Martin Hundeb√∏ll <martin@hundeboll.net>

pkgname=tio
pkgver=3.6
pkgrel=1
pkgdesc="The simple TTY terminal I/O application"
url="https://github.com/tio/tio"
arch=('x86_64' 'i686' 'arm' 'armv7h' 'armv6h' 'aarch64')
license=('GPL-2.0-or-later')
depends=(
  'glib2'
  'glibc'
  'lua'
)
makedepends=(
  'bash-completion'
  'meson'
)
source=("https://github.com/tio/tio/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz")
b2sums=('52152a133b497e7f39f0bb48348ef869ad5fa2135f323796ef16f0caf7c8b3bb5dc38dde7b40c858bd27a1e3addbb97911eef219be5f8b9885638389cd45f9e2')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

package() {
  depends+=('libglib-2.0.so')
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
