# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=bluez-utils-old
pkgver=5.72
pkgrel=2
pkgdesc="deprecated tools from bluez"
url="http://www.bluez.org/"
arch=('i686' 'x86_64' 'mips64el' 'armv6h' 'armv7h' 'arm' 'aarch64' 'riscv64')
license=('GPL-2.0-only')
depends=(
         'glibc'
         'systemd-libs' 'libudev.so'
         'glib2' 'libglib-2.0.so'
         )
makedepends=('dbus' 'libical' 'systemd' 'alsa-lib' 'json-c' 'ell' 'python-docutils' 'zsh' 'bluez-libs' 'python-pygments')
conflicts=('bluez-hciattach' 'bluez-hciconfig' 'bluez-hcitool' 'bluez-rfcomm' 'bluez-sdptool' 'bluez-ciptool' 'bluez-gatttool') # bluz-utils provides bluez-hcidump?
provides=('bluez-hciattach' 'bluez-hciconfig' 'bluez-hcitool' 'bluez-rfcomm' 'bluez-sdptool' 'bluez-ciptool' 'bluez-gatttool')
source=(https://www.kernel.org/pub/linux/bluetooth/bluez-${pkgver}.tar.{xz,sign})
b2sums=('b5d1bc290c9bad5aa32de16433c34d0ac5aed7164077ef941a0bf90eddc5e22a6cf19f972bcae33061b41e3c344f7abeb65a11b6013c13fd53059ac93d83240a'
        'SKIP')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659') # Marcel Holtmann <marcel@holtmann.org>

build() {
  cd "bluez-${pkgver}"
  ./configure \
          --prefix=/usr \
          --mandir=/usr/share/man \
          --sysconfdir=/etc \
          --localstatedir=/var \
          --libexecdir=/usr/lib \
          --with-dbusconfdir=/usr/share \
          --enable-tools \
          --disable-cups \
          --disable-monitor \
          --disable-midi \
          --disable-mesh \
          --enable-experimental \
          --enable-library \
          --enable-deprecated # to enable deprectated tools
  make
}

package() {
  cd "bluez-${pkgver}"
  make DESTDIR="${pkgdir}" \
       install-binPROGRAMS \
       install-man1
       
  cat <<EOF | zsh -
  old_utils=(hciattach hciconfig hcitool hcidump rfcomm sdptool ciptool gatttool)
  for i in "${pkgdir}"/usr/bin/* ; do
    (( \$old_utils[(I)\${i:t}] )) || rm \$i
  done
  for i in "${pkgdir}"/usr/share/man/man1/* ; do
    (( \$old_utils[(I)\${i:t:r}] )) || rm \$i
  done
EOF
  
  rm -rf "${pkgdir}"/usr/lib
}

