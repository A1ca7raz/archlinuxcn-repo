# Maintainer: Coleacanthus <CoelacanthusHex@gmail.com>

_pkgname=globalplatformpro
pkgname=${_pkgname}-git
pkgver=21.12.31.r55.g52c3581
pkgrel=1
pkgdesc="Manage applets and keys on JavaCard-s like a pro"
arch=('any')
url="https://github.com/martinpaljak/GlobalPlatformPro"
license=('LGPL3')
depends=('java-runtime-headless=17' 'pcsclite')
makedepends=('git' 'java-environment=17' 'maven' 'which')
conflicts=('globalplatformpro')
provides=('globalplatformpro')
source=(
  "$_pkgname::git+https://github.com/martinpaljak/GlobalPlatformPro.git"
  'gpp'
)
b2sums=('SKIP'
        'c6c137f8b47e29daab8b343f42ade1573b2d8e43e17fbbf054f8e6b7069e4476b7cafd6202f5b133ee11b99b406447441f90bdb441039e142c10fddff54d205f')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_pkgname
}

build() {
  cd $_pkgname
  ./mvnw package # Use maven wrapper because Arch Linux has too old maven in repo
}

package() {
  install -d "$pkgdir"/usr/bin/
  install -m 755 gpp "$pkgdir"/usr/bin/
  cd $_pkgname
  install -d "$pkgdir"/usr/share/java/globalplatformpro/
  install -m 644 tool/target/gp.jar "$pkgdir"/usr/share/java/globalplatformpro/
}
