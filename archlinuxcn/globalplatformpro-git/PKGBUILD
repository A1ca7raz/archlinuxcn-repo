# Maintainer: Coleacanthus <CoelacanthusHex@gmail.com>

_pkgname=globalplatformpro
pkgname=${_pkgname}-git
pkgver=21.12.31.r55.g52c3581
pkgrel=2
pkgdesc="Manage applets and keys on JavaCard-s like a pro"
arch=('any')
url="https://github.com/martinpaljak/GlobalPlatformPro"
license=('LGPL-3.0-or-later AND MIT')
depends=('java-runtime-headless>=17' 'pcsclite' 'bash')
makedepends=('git' 'java-environment=17' 'maven' 'which')
conflicts=('globalplatformpro')
provides=('globalplatformpro')
source=(
  "$_pkgname::git+https://github.com/martinpaljak/GlobalPlatformPro.git"
  'gpp'
)
b2sums=('SKIP'
        '0fd110a0431ab4bf35adc83ff7205c7eae71ea8cda04520a01e7aa5c045010282357ad9d61dcee414728a225fad640fc4275165bd05457f451de76437bbae976')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_pkgname
}

build() {
  cd $_pkgname
  ./mvnw package # FIXME: Use maven wrapper because Arch Linux has too old maven in repo
}

package() {
  install -d "$pkgdir"/usr/bin/
  install -m 755 gpp "$pkgdir"/usr/bin/
  cd $_pkgname
  install -d "$pkgdir"/usr/share/java/globalplatformpro/
  install -m 644 tool/target/gp.jar "$pkgdir"/usr/share/java/globalplatformpro/
  install -d "$pkgdir"/usr/share/licenses/"$pkgname"
  install -m 644 README.md "$pkgdir"/usr/share/licenses/"$pkgname"
}
