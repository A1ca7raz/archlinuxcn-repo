# Maintainer: noodlefighter <noodlefighter#gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: GONG Chen <chen dot sst at gmail dot com>
# Contributor: 網軍總司令

_pkgname=librime
pkgname=$_pkgname-sbxlm-git
pkgver=9.5.18.r0.g4177b219
_octagramcommit=f92e083052b9983ee3cbddcda5ed60bb3c068e24
_luacommit=d45a41af2f9d731e3c1516a191cc3160e3cb8377
pkgrel=1
epoch=2
pkgdesc="Rime input method engine (声笔系列码 GIT version)"
arch=('x86_64')
url="https://github.com/sbxlmdsl/librime"
license=('BSD-3-Clause')
depends=('boost-libs' 'opencc' 'yaml-cpp' 'gcc-libs' 'glibc' 'leveldb' 'lua' 'google-glog' 'marisa')
makedepends=('cmake' 'boost' 'git' 'gtest' 'ninja')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://github.com/sbxlmdsl/librime.git"
        "https://github.com/lotem/librime-octagram/archive/$_octagramcommit/librime-octagram-$_octagramcommit.tar.gz"
        "https://github.com/hchunhui/librime-lua/archive/$_luacommit/librime-lua-$_luacommit.tar.gz"
        "./001-fix-warning.patch")
sha512sums=('SKIP'
            '737d1c58982d2f79a6e8b2548eefa1dddc036dd6e6d5436e7d6b4f3adfa2e9d8e45b29a13c1b8207a93cb77f3b5dbd9d18436f44d4e8040eb95b962de582b386'
            '2a3d3b49d53066fe96dd008e8064718082225e6bf185574a25b8e98175d9936abcfa1fdc56e48f9c72a2deb46f8157d6132fd119ff8e0a3d52fbe9e2ea21386c'
            'acff0ec596ddc66e3a6341ba92b33bdda7fe08573ce045bd5ac1223978c3a907239d2ad7b6a5d479a5ea82735a205e3312c5b7e5a2101cac40226d51b26eedf1')

prepare() {
  cd $_pkgname
  git apply ${srcdir}/001-fix-warning.patch 
  cd plugins
  ln -sf "$srcdir"/librime-octagram-$_octagramcommit librime-octagram
  ln -sf "$srcdir"/librime-lua-$_luacommit librime-lua
}

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/^rime-//;s/_/./;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname
  export CXXFLAGS="$CXXFLAGS -DNDEBUG"
  cmake . -GNinja -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_MERGED_PLUGINS=Off -DENABLE_EXTERNAL_PLUGINS=On
  cmake --build build
}

check() {
  cd $_pkgname/build
  ninja test
}

package() {
  cd $_pkgname/build
  DESTDIR="$pkgdir" ninja install
  install -Dm644 "${srcdir}/${_pkgname}/LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
