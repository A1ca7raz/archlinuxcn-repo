# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

_pkgname=scx
pkgname="${_pkgname}-scheds"
pkgver=0.1.10
pkgrel=1
pkgdesc="Sched_ext schedulers"
url="https://github.com/sched-ext/${_pkgname}"
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libbpf' 'bpf' 'libelf' 'zlib' 'systemd')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'cargo' 'jq')
backup=('etc/default/scx')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('af9d6a2260526b34df00c8c89edf66b5ec8d8ca73537aee22ae2f5b0c82447cb4383f7a86423c2296f7949af57e632cc0194f585608765970ce68f807deee18d')
options=('!lto')

build() {
	cd "${_pkgname}-${pkgver}/"
	arch-meson . build -Dcargo_home=$HOME/cargo-deps --auto-features auto
	meson compile -C build
}

package() {
	cd "${_pkgname}-${pkgver}/"
	meson install -C build --destdir "${pkgdir}"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
