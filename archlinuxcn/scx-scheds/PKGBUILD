# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

_pkgname=scx
pkgname="${_pkgname}-scheds"
pkgver=0.1.6
pkgrel=1
pkgdesc="Sched_ext schedulers"
url="https://github.com/sched-ext/${_pkgname}"
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libbpf' 'bpf' 'libelf' 'zlib' 'systemd')
makedepends=('python' 'meson' 'clang' 'llvm-libs' 'cargo')
backup=('etc/default/scx')
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('84dfc711ba9d51cd26c104caca1dcee3ce0be790be5b3a294ed47f4ef5fb20c58524b793d45b0970d61befa33adf8c8f3ca2b2f726d2ef335c80c11eee813387')
options=('!lto')

build() {
	arch-meson "${_pkgname}-${pkgver}/" build -Dcargo_home=$HOME/cargo-deps
	meson compile -C build
}

package() {
	meson install -C build --destdir "${pkgdir}"
	install -Dm644 "${_pkgname}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
