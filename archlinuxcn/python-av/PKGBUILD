# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: peippo <christoph.fink@gmail.com>

pkgname=python-av
_pkgname=PyAV
pkgdesc="Pythonic bindings for FFmpeg's libraries"
url='https://pyav.org'
pkgver=12.1.0
pkgrel=1
arch=('x86_64')
license=('BSD-3-Clause')
makedepends=(
  cython
  gcc13
  python-build
  python-installer
  python-setuptools
  python-wheel
)
depends=(
  ffmpeg
  python-numpy
  python-pillow
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PyAV-Org/PyAV/archive/v${pkgver}.tar.gz")
sha512sums=('9b1722f1f4fa5623a5c9984fc71dda956f1838cab9a4574eb156499f9e8c2b1b516a6bbcee0d5e3f1d0b8ce4725099371900e7facad76a8998737638ff4e9a4d')

build() {
  cd "${_pkgname}-${pkgver}"
  CC=gcc-13 \
  CXX=g++-13 \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:

