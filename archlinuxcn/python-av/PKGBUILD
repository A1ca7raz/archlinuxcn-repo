# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: peippo <christoph.fink@gmail.com>

pkgname=python-av
_pkgname=PyAV
pkgdesc="Pythonic bindings for FFmpeg's libraries"
url='https://pyav.org'
pkgver=12.2.0
pkgrel=1
arch=('x86_64')
license=('BSD-3-Clause')
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools
  python-wheel
)
depends=(
  ffmpeg6.1
  glibc
  python-numpy
  python-pillow
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PyAV-Org/PyAV/archive/v${pkgver}.tar.gz")
sha512sums=('e7a2756a5734dccea4d8b6166cc3b44883c3fbc7dcf884b846c4b1262c051f57935a32705b92d05f46d6682aae3499495d90aac98b0b4880b4f7776d77b6fe6e')

build() {
  cd "${_pkgname}-${pkgver}"
  PKG_CONFIG_PATH="/usr/lib/ffmpeg6.1/pkgconfig/" \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
