# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: peippo <christoph.fink@gmail.com>

pkgname=python-av
_pkgname=PyAV
pkgdesc="Pythonic bindings for FFmpeg's libraries"
url='https://pyav.org'
pkgver=11.0.0
pkgrel=1
arch=('x86_64')
license=('BSD')
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools
  python-wheel
)
depends=(
  ffmpeg4.4
  python-numpy
  python-pillow
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PyAV-Org/PyAV/archive/v${pkgver}.tar.gz")
sha512sums=('52791ec2823f58daa05475b8422733a49361c9d9072ce71815fc58edb78e2eed6c963e1e656d16d190786260432866953393ee9c9a65348131f3099e941da8b6')

build() {
  cd "${_pkgname}-${pkgver}"
  # https://github.com/PyAV-Org/PyAV/issues/817
  PKG_CONFIG_PATH=/usr/lib/ffmpeg4.4/pkgconfig \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:

