# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Solomon Choina <shlomochoina@gmail.com0
pkgname=wayfire-lily-git
pkgver=0.9.0.20231207.065445.g3b3be365
pkgrel=1
pkgdesc="3D wayland compositor (lilydjwg's version)"
arch=('x86_64')
url="https://github.com/lilydjwg/wayfire"
license=('MIT')
depends=('wlroots-lily-git' 'cairo' 'pango' 'glm' 'libjpeg' 'libdisplay-info' 'libliftoff')
makedepends=('git' 'meson' 'ninja' 'wayland-protocols' 'doctest')
optdepends=()
provides=(wayfire wayfire-git)
conflicts=(wayfire wayfire-git)
options=(debug)
source=(wayfire::git+$url.git)
sha256sums=(SKIP)

pkgver () {
    cd "$srcdir/wayfire"
    (
        set -o pipefail
        version=$(grep -zoP "project\([^)]*\)" meson.build | xargs --null echo | grep -oP "^\sversion:\s'\K[^']*")
        printf "${version}.%s.g%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)" "$(git log -1 --format='%h')"
    )
}

build() {
    cd "$srcdir/wayfire"
    arch-meson \
        --buildtype=debug \
        -Dtests=disabled \
        -Duse_system_wfconfig=disabled \
        -Duse_system_wlroots=enabled \
        -Dprint_trace=false \
        . \
        build
    ninja -C build
}

package() {
    cd "$srcdir/wayfire"
    DESTDIR="${pkgdir}/" ninja -C build install
    install -m644 wayfire.ini "${pkgdir}/usr/share"
}
