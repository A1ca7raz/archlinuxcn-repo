# Maintainer: Evgeniy Dombek <edombek@yandex.ru>
# Maintainer:  Yigit Dallilar <yigit.dallilar at gmail dot com>

pkgname=ds9
pkgver=8.6b1
pkgrel=1
pkgdesc="SAOImage DS9: Astronomical Data Visualization Application"
url="https://sites.google.com/cfa.harvard.edu/saoimageds9"
arch=('x86_64')
license=('GPL2')
provides=(ds9)
conflicts=(ds9-bin)
depends=("desktop-file-utils" "libx11" "libxft" "libxml2" "libxslt" "tcl" "tk" "zlib")
options=(!strip !lto)
makedepends=(gcc make automake autoconf zip)
replaces=()
backup=()
source=("https://github.com/SAOImageDS9/SAOImageDS9/archive/v${pkgver}.tar.gz"
        "ds9.desktop")
sha1sums=(
'1475b13669d4ae2f15b0a32cc01accf08981e4d1'
'84f033877023f70625bd111a8670d88d00c2bfbc'
)


build() {
    cd ${srcdir}/SAOImageDS9-${pkgver}
    unix/configure
    # Suppress warning, until commit 8d30858 is included in the release.
    CFLAGS="-Wno-incompatible-pointer-types" make
}

package() {
    
    install -Dm644 ${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
    install -Dm644 ${srcdir}/SAOImageDS9-${pkgver}/ds9/doc/sun.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png

    cd ${srcdir}/SAOImageDS9-${pkgver}
    install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    install -Dm755 bin/ds9 ${pkgdir}/usr/bin/${pkgname}

}

# vim:set ts=4 sw=4 et:

