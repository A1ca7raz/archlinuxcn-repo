# Maintainer: Kiri <kiri@vern.cc> 
# Contributor: Igor Dyatlov <dyatlov.igor@protonmail.com>

pkgname=amberol
pkgver=0.10.3
pkgrel=1
pkgdesc="Plays music, and nothing else"
arch=(x86_64)
url="https://gitlab.gnome.org/World/amberol"
license=('GPL-3.0-or-later')
depends=(cairo
         dbus
         dconf
         gcc-libs
         gdk-pixbuf2
         glib2
         glibc
         graphene
         gst-plugins-bad-libs
         gst-plugins-base-libs
         gstreamer
         gtk4
         hicolor-icon-theme
         libadwaita
         pango)
makedepends=(cargo
             meson)
checkdepends=(appstream-glib
              desktop-file-utils
              reuse)
_archive="$pkgname-$pkgver"
source=("$_archive.tar.gz::$url/-/archive/$pkgver/$_archive.tar.gz")
options=('!lto')
sha512sums=('12684a222e06d0abbab03f6196021c262416ca9ed48526dd5343432a314c2615866ad6b97032c45d93003bb198a2bc90cb23baaf7debe3d3581cb3fd925021cf')

prepare() {
  cd $_archive
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  arch-meson $_archive build --buildtype=release
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
