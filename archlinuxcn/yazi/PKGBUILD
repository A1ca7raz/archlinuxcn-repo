# Maintainer: Pig Fang <g-plane@hotmail.com>

pkgname=yazi
pkgver=0.1.4
pkgrel=1
pkgdesc="Blazing fast terminal file manager written in Rust, based on async I/O."
url="https://github.com/sxyazi/yazi"
arch=("x86_64" "aarch64" "riscv64")
license=("MIT")
depends=("ttf-nerd-fonts-symbols")
optdepends=(
	'jq: for JSON preview'
	'unarchiver: for archive preview'
	'ffmpegthumbnailer: for video thumbnails'
	'fd: for file searching'
	'ripgrep: for file content searching'
	'fzf: for directory jumping'
	'poppler: for PDF preview'
	'zoxide: for directory jumping')
makedepends=("cargo")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sxyazi/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('991f8621cf6f362d0c22cd598a434b6fb42fb7e9fde4c2f678297f9ac4959d36')
options=('!lto')

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --release
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm644 "LICENCE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
}
