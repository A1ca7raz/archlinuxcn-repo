# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Megumi_fox <megumifox@archlinuxcn.org>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: hexchain <i@hexchain.org>
pkgname=telegram-desktop-lily
_pkgname=telegram-desktop
pkgver=4.16.2
pkgrel=1
pkgdesc="Official Telegram Desktop client with megumifox & lily's patch"
arch=('x86_64')
url="https://desktop.telegram.org/"
license=('GPL-3.0-only WITH x11vnc-openssl-exception')
depends=('hunspell'
         'ffmpeg' 'libavfilter.so' 'libavformat.so' 'libavcodec.so' 'libavutil.so'
         'hicolor-icon-theme' 'lz4' 'minizip' 'openal'
         'qt6-imageformats' 'qt6-svg' 'qt6-wayland' 'xxhash' 'glibmm-2.68'
         'rnnoise' 'pipewire' 'libxcomposite' 'libxdamage' 'libxtst' 'libxrandr' 'jemalloc' 'abseil-cpp' 'libdispatch'
         'openssl' 'libcrypto.so' 'libssl.so'
         'protobuf' 'libprotobuf-lite.so'
         'libvpx' 'libvpx.so')
makedepends=('cmake' 'git' 'ninja' 'python' 'range-v3' 'tl-expected' 'microsoft-gsl' 'meson'
             'extra-cmake-modules' 'wayland-protocols' 'plasma-wayland-protocols' 'libtg_owt'
             'gobject-introspection' 'glib2' 'boost' 'fmt' 'python-packaging')
optdepends=('webkit2gtk: embedded browser features'
            'xdg-desktop-portal: desktop integration')
provides=('telegram-desktop')
conflicts=('telegram-desktop')
source=("https://github.com/telegramdesktop/tdesktop/releases/download/v${pkgver}/tdesktop-${pkgver}-full.tar.gz"
        "0001-Use-font-from-environment-variables.patch"
        "0002-add-TDESKTOP_DISABLE_REGISTER_CUSTOM_SCHEME-back.patch"
        "0003-make-blockquote-markup-less-distractive.patch"
        "0004-Drop-usage-of-Qt-6.5-colorScheme-API-on-Linux.patch"
        "warn-before-delete-all-2.patch")
sha512sums=('990999bef3edb09709fb2faf4b9b3c6ffb43460172f7ccaf2e30e514a0688a869bb0eb74aa671b8cb3a2c9971304371fc5c0218b3b4e5bfa8eed4414680bcf41'
            '13b1b0f34b0937a98aa61fed776318717c659358794c0ffa8b1af00c5c80fe195a11816db9183eee8e8e02f76c72f10c723093a24a9c0de249af18dcf2755126'
            '70c09627e13e01a8caac807738435ccbb39926ef7bc7517dc1f32c83a9c9fb49db1d0f3203fe6154566f032f9c9784ad3cf920bb27dd44f4630fc794d7b40f77'
            '6464564292143033d74c029e000b3af80ad2f20a3bbd908ffe3fd2f6d6f10da76ba2e6850029aa1f43f8a2d94572be140ee3759e4e4cd51fd73f1a8096f26a7e'
            'c67c1123042efc6515fb821b6438edcfc6d77dd08fd82c8513e4e0b0da7c0ee3e9f9ea0118d65584b7c837b906a8eb9759c125a9298d3b4b1e4491357f3eccb1'
            '891a58e7d1a4d4a27a17a2b56bd614d87d22322f2f6e1247665b2b873c5c6d78336de04bc7a11feceb7cd8f1a2f79a858ad2520352644e808634e2989a17357d')

prepare() {
    patch -b -d tdesktop-$pkgver-full/Telegram/lib_ui/ -Np1 -i ${srcdir}/0001-Use-font-from-environment-variables.patch
    patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0002-add-TDESKTOP_DISABLE_REGISTER_CUSTOM_SCHEME-back.patch
    patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0003-make-blockquote-markup-less-distractive.patch
    patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0004-Drop-usage-of-Qt-6.5-colorScheme-API-on-Linux.patch
    patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/warn-before-delete-all-2.patch
}

build() {
    CXXFLAGS+=' -ffat-lto-objects'
    # Turns out we're allowed to use the official API key that telegram uses for their snap builds:
    # https://github.com/telegramdesktop/tdesktop/blob/8fab9167beb2407c1153930ed03a4badd0c2b59f/snap/snapcraft.yaml#L87-L88
    # Thanks @primeos!
    cmake -B build -S tdesktop-$pkgver-full -G Ninja \
        -DCMAKE_VERBOSE_MAKEFILE=ON \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_BUILD_TYPE=Release \
        -DTDESKTOP_API_ID=611335 \
        -DTDESKTOP_API_HASH=d524b414d21f4d37f08684c1df41ac9c \
        -DTDESKTOP_DISABLE_REGISTER_CUSTOM_SCHEME=ON
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
