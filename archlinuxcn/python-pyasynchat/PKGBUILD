# Maintainer: Chih-Hsuan Yen <base64_decode("eXUzYWN0eHQydHR0ZmlteEBjaHllbi5jYwo=")>

pkgname=python-pyasynchat
pkgver=1.0.4
pkgrel=3
pkgdesc='Make asynchat available for Python 3.12 onwards'
arch=(any)
url='https://github.com/simonrob/pyasynchat/'
# https://github.com/simonrob/pyasynchat/blob/v1.0.4/setup.py#L22
license=('PSF-2.0')
depends=(python python-pyasyncore)
makedepends=(git python-build python-installer python-setuptools python-wheel)
# Tests are extracted from CPython, and thus CPython test utilities are used
checkdepends=(python-tests)
# Not checking git signatures, as only commits are signed, not tags,
# and commit verification requires manual efforts during bumping versions
source=("git+https://github.com/simonrob/pyasynchat.git#tag=v$pkgver")
sha256sums=('81ae47adb19fe3436fffecaa409fcd6f9656ab9d9efdea8ad3458d8393e828b9')

build() {
  cd pyasynchat
  python -m build --wheel --no-isolation
}

check() {
  cd pyasynchat
  python -m unittest discover ./tests
}

package() {
  cd pyasynchat
  python -m installer --destdir="$pkgdir" dist/*.whl
}
