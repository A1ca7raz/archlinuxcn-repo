# Maintainer: randomnobody <nobody "at" 420blaze "dot" it>
# Contributor: Finlay Maroney <finman292004@protonmail.com>

_target=i686-elf
_pkgname=binutils
pkgname="${_target}-${_pkgname}"
pkgver=2.42
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files for the i686-elf target"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/${_pkgname}"
license=('GPL-2.0-or-later')
depends=('zlib' 'libelf')
source=(https://mirrors.kernel.org/gnu/${_pkgname}/${_pkgname}-${pkgver}.tar.xz{,.sig})
sha256sums=('f6e4d41fd5fc778b06b7891457b3620da5ecea1006c6a4a41ae998109f85a800'
            'SKIP')
validpgpkeys=(3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F) # Nick Clifton (Chief Binutils Maintainer) <nickc@redhat.com>
options=('!emptydirs' '!docs')

prepare() {
	cd "${_pkgname}-${pkgver}/"
	mkdir build
}

build() {
	cd "${_pkgname}-${pkgver}/build/"

	../configure \
		--target=${_target} \
		--with-sysroot \
		--prefix=/usr \
		--bindir=/usr/bin \
		--libdir=/usr/lib/i686-elf/ \
		--disable-nls \
		--disable-werror

	make
}

check() {
	cd "${_pkgname}-${pkgver}/build/"

	# do not abort on errors - manually check log files
	make -k check
}

package() {
	cd "${_pkgname}-${pkgver}/build/"
	make DESTDIR="${pkgdir}" install
	rm -rf ${pkgdir}/usr/share/{info,man}
}
