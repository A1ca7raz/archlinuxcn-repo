pkgname=julia-git-recursivefactorization-src
pkgver=0.2.21
_commit=518200896214420bf3bf584a55af9cd1d04d57c6
pkgrel=3
pkgdesc="RecursiveFactorization.jl"
url="https://github.com/JuliaLinearAlgebra/RecursiveFactorization.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaLinearAlgebra/RecursiveFactorization.jl.git#commit=$_commit"
        0001-don-t-pick-threshold-based-on-register_size.patch
        0002-Replace-LoopVectorization-dependency-with-Vectorizat.patch)
sha256sums=('SKIP'
            '242eb8d71f0b32ca79ea5ab8c1bbeba58ae98233c85a9597f76200b2367b1e0a'
            '07d9545d9015e3d39022f9ef93ae7ec95739c40cee85a9c38d8e8cbbabffd5f8')

prepare() {
  cd RecursiveFactorization.jl

  patch -Np1 --no-backup-if-mismatch < ../0001-don-t-pick-threshold-based-on-register_size.patch
  patch -Np1 --no-backup-if-mismatch < ../0002-Replace-LoopVectorization-dependency-with-Vectorizat.patch
}

package() {
  cd RecursiveFactorization.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh RecursiveFactorization "${pkgdir}" "${pkgname}" julia-git
}
