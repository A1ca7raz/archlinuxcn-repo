# Maintainer: Metal A-wing <1 at 233 dot email>

_name=iredis
pkgname=iredis
pkgver=1.14.0
pkgrel=1
pkgdesc="Interactive Redis: A Terminal Client for Redis with AutoCompletion and Syntax Highlighting."
arch=(any)
url="https://github.com/laixintao/iredis"
license=('BSD-3-Clause')
depends=('python' 'python-redis' 'python-packaging' 'python-prompt_toolkit' 'python-pygments' 'python-mistune' 'python-configobj' 'python-click' 'python-pendulum')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel' 'python-poetry')
source=("${_name}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/iredis-${pkgver}.tar.gz")
sha256sums=('821336ab54e4cea7169ac51bd94f3daa45a3b9843b1adee1bf2ea3dd3230f184')

build() {
  cd "${_name}-${pkgver}"
  python3 -m build --wheel --no-isolation
}

package() {
  cd "${_name}-${pkgver}"
  python3 -m installer --destdir="${pkgdir}" dist/*.whl
  
  # license
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
