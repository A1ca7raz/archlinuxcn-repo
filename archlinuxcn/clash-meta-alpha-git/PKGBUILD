# Maintainer: sukanka <su975853527 at gmail dot com>
# Maintainer: Roald Clark <roaldclark at gmail dot com>

pkgname=clash-meta-alpha-git
_pkgname=clash-meta
pkgver=20240326.r0.g0b4662e4
pkgrel=2
pkgdesc="Another Clash Kernel (git virsion)"
arch=(x86_64)
url="https://github.com/MetaCubeX/mihomo"
license=(GPL-3.0-or-later)
depends=(clash-geoip glibc)
makedepends=(git go)
provides=(clash-meta clash-meta-alpha)
conflicts=(clash-meta clash-meta-alpha)
backup=('etc/clash-meta/config.yaml')
source=()
sha256sums=()

prepare() {
    cd $srcdir
    git clone --depth 1 --branch Alpha $url $pkgname
}

pkgver() {
    cd "$srcdir/$pkgname"
    git log --pretty=format:"%ad.%h" --date=format:"%Y%m%d"
}

build() {
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_ENABLED=1
    cd "$srcdir/$pkgname"
    BUILDTIME=$(date -u)
    GOOS=linux go build \
        -trimpath \
        -buildmode=pie \
        -mod=readonly \
        -modcacherw \
        -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" \
        -X \"github.com/metacubex/mihomo/constant.Version=${pkgver}\" \
        -X \"github.com/metacubex/mihomo/constant.BuildTime=${BUILDTIME}\" \
        " \
        -tags with_gvisor -o $pkgname-$pkgver

    echo "u $_pkgname - \"Clash-Meta Service\" - -"        >  "$_pkgname.sysusers"
    echo "d /etc/$_pkgname     0755 $_pkgname $_pkgname -" >  "$_pkgname.tmpfiles"
    echo "d /var/log/$_pkgname 0700 $_pkgname $_pkgname -" >> "$_pkgname.tmpfiles"
    sed -i -e '/Description/s/mihomo/Clash-Meta/' \
           -e "s/mihomo/$_pkgname/g"              \
           -e "/^Type=simple/a User=$_pkgname"    \
           -e "/^Type=simple/a Group=$_pkgname"   .github/mihomo.service
}

check() {
    cd "$srcdir/$pkgname"
    go test ./...
}

package() {
    cd "$srcdir/$pkgname"
    install -Dm755 "$pkgname-$pkgver"      "$pkgdir/usr/bin/clash-meta"
    install -Dm644 "$_pkgname.sysusers"    "$pkgdir/usr/lib/sysusers.d/$_pkgname.conf"
    install -Dm644 "$_pkgname.tmpfiles"    "$pkgdir/usr/lib/tmpfiles.d/$_pkgname.conf"
    install -Dm644 docs/config.yaml        "$pkgdir/etc/clash-meta/config.yaml"
    install -Dm644 .github/mihomo.service  "$pkgdir/usr/lib/systemd/system/clash-meta.service"
    ln -s          /etc/clash/Country.mmdb "$pkgdir/etc/$_pkgname/Country.mmdb"
}
