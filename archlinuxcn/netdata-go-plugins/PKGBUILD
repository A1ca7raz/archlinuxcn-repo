# Maintainer: hexchain <i at hexchain dot org>

pkgname=netdata-go-plugins
pkgver=0.58.1
pkgrel=1
pkgdesc="netdata go.d plugin"
url="https://github.com/netdata/go.d.plugin"
license=('GPL-3.0-or-later')
arch=('x86_64')
depends=('glibc')
makedepends=('go')
install=netdata-go-plugins.install
source=(
	"${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
	"${pkgname}-${pkgver}-config.tar.gz::${url}/releases/download/v${pkgver}/config.tar.gz")
sha512sums=('acbfd7ee4a72cee0587936bd2d4e27a2b58894ee72a2218b96643c1fe0866990f9d6bfe1f754cda5f5443bc75e1406a6511300ea265a81e4be89f7f97b17664c'
            'd5a608ca580524a398746e7790f11b8045f1ba85be8d595e8e4e3b59efeeda4cbaf2f8b7a98d1c6f3843f98ce696a531f891cba5ab16a5f2ef36d4bd969db4a0')

prepare() {
	mkdir build
	export GOPATH="${srcdir}/build/"
	export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw"

	cd "go.d.plugin-${pkgver}/"
	go mod download
}

build() {
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOPATH="${srcdir}"
	export GOLDFLAGS="-linkmode=external -compressdwarf=false -X main.version=${pkgver}"
	export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw"

	cd "go.d.plugin-${pkgver}/"
	go build -v -ldflags="$GOLDFLAGS" -o go.d.plugin ./cmd/godplugin
}

package() {
	install -d "${pkgdir}/usr/lib/netdata/conf.d/"
	cp -rv --no-preserve=ownership go.d{,.conf} "${pkgdir}/usr/lib/netdata/conf.d/"
	install -Dm755 "go.d.plugin-${pkgver}/go.d.plugin" -t "${pkgdir}/usr/lib/netdata/plugins.d/"

	cd "go.d.plugin-${pkgver}/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -d "${pkgdir}/usr/share/doc/${pkgname}/"
	cp docs/* "${pkgdir}/usr/share/doc/${pkgname}/"
}
