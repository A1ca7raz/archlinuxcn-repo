# Maintainer: acxz <akashpatel2008 at yahoo dot com>

_pkgname=opencensus-python
pkgname=python-opencensus
pkgver=0.11.3
pkgrel=1
pkgdesc='A stats collection and distributed tracing framework'
arch=('any')
url='https://github.com/census-instrumentation/opencensus-python'
license=('Apache')
depends=(
  python-google-api-core
)
makedepends=(
  python-setuptools
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/census-instrumentation/opencensus-python/archive/v${pkgver}.tar.gz")
sha256sums=('495f9aa7e49cfff040f5fb7d02370362ba85fcc1aa9c28c659565d63f50ed728')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/context/opencensus-context"
  python setup.py build

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/context/opencensus-context"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
