pkgname=mosh-git
epoch=1
pkgver=1.4.0.r19.g1105d48
pkgrel=4
pkgdesc='Mobile shell, surviving disconnects with local echo and line editing. Git version'
arch=(x86_64 aarch64)
url='https://mosh.org/'
license=(GPL3)
depends=(libutempter openssh openssl perl-io-tty protobuf)
makedepends=(git)
conflicts=(mosh)
provides=(mosh)
options=(!emptydirs)
source=("git+https://github.com/mobile-shell/mosh"
        https://github.com/mobile-shell/mosh/pull/1292.diff)
sha512sums=('SKIP'
            '8378b9c38fcc395aa8d68b0efd756769bcd6e0ffa2e3a41caa4f8dacd72cd95bf7daba8c4bbade7812ad003a2f8cf20152825f4ebb619f7d333802a15e523a64')

pkgver() {
  cd "$srcdir/mosh"
  git describe | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^mosh\.//'
}

prepare() {
  cd "$srcdir/mosh"
  patch -Np1 < ../1292.diff
}

build() {
  cd "$srcdir/mosh"
  ./autogen.sh
  ./configure --enable-ufw --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/mosh"
  DESTDIR="$pkgdir" make install
  install -Dm644 "conf/bash-completion/completions/mosh" \
    "$pkgdir/usr/share/bash-completion/completions/mosh"
}
