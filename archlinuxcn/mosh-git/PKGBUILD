pkgname=mosh-git
epoch=1
pkgver=1.4.0.r19.g1105d48
pkgrel=2
pkgdesc='Mobile shell, surviving disconnects with local echo and line editing. Git version'
arch=(x86_64 aarch64)
url='https://mosh.org/'
license=(GPL3)
depends=(libutempter openssh openssl perl-io-tty protobuf)
makedepends=(git)
conflicts=(mosh)
provides=(mosh)
options=(!emptydirs)
source=("git+https://github.com/mobile-shell/mosh"
        1167.patch)
sha512sums=('SKIP'
            'cf83e1357eefb8bdf1542850d66d8007d620e4050b5715dc83f4a921d36ce9ce47d0d13c5d85f2b0ff8318d2877eec2f63b931bd47417a81a538327af927da3e')

pkgver() {
  cd "$srcdir/mosh"
  git describe | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^mosh\.//'
}

prepare() {
  cd "$srcdir/mosh"
  patch -Np1 < ../1167.patch
}

build() {
  cd "$srcdir/mosh"
  ./autogen.sh
  ./configure --enable-ufw --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/mosh"
  DESTDIR="$pkgdir" make install
  install -Dm644 "conf/bash-completion/completions/mosh" \
    "$pkgdir/usr/share/bash-completion/completions/mosh"
}
