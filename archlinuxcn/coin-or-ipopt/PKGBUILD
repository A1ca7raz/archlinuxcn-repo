pkgname=coin-or-ipopt
pkgver=3.14.12
pkgrel=1
pkgdesc="Interior Point OPTimizer"
arch=('aarch64' 'armv7h' 'x86_64')
url="https://github.com/coin-or/Ipopt"
license=('EPL')
depends=('lapack64' 'spral' 'coin-or-asl' 'mumps-seq')
source=("https://github.com/coin-or/Ipopt/archive/refs/tags/releases/${pkgver}.tar.gz")
sha256sums=('6b06cd6280d5ca52fc97ca95adaaddd43529e6e8637c274e21ee1072c3b4577f')

build() {
  cd "$srcdir"
  mkdir -p build && cd build

  ../Ipopt-releases-$pkgver/configure \
    CFLAGS="$CFLAGS -O3 -DNDEBUG" \
    CXXFLAGS="$CXXFLAGS -O3 -DNDEBUG" \
    FFLAGS="$FFLAGS -O3" \
    FCFLAGS="$FCFLAGS -O3" \
    --prefix=/usr \
    --enable-shared \
    --enable-static \
    --with-pic \
    --disable-dependency-tracking \
    lt_cv_deplibs_check_method=pass_all \
    --with-lapack-lflags="-llapack64" \
    --with-spral-lflags="-lspral" \
    --with-mumps-cflags="-I/usr/include/mumps-seq -I/usr/include/mpiseq" \
    --with-mumps-lflags="-ldmumps_seq -lzmumps_seq -lcmumps_seq -lsmumps_seq -lmumps_common_seq -lmpiseq -lpord_seq -lmetis -llapack64 -lgfortran -lpthread" \
    --with-asl-cflags="-I/usr/include/coin-or/asl" \
    --with-asl-lflags="-lcoinasl -lrt"

  make -j1
}

package() {
  cd "$srcdir/build"
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
                   make DESTDIR="$pkgdir" install
}
