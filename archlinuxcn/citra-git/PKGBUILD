# Maintainer: Tim Schumacher <timschumi@gmx.de>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: ceri

_pkgbase='citra'
pkgname="$_pkgbase-git"
pkgver=r9745.cf54210f4
pkgrel=1
pkgdesc="An experimental open-source Nintendo 3DS emulator/debugger (Master branch)"
arch=('i686' 'x86_64')
url="https://github.com/citra-emu/citra/"
license=('GPL2')
depends=('boost-libs' 'shared-mime-info' 'ffmpeg' 'libusb' 'sdl2' 'qt6-multimedia' 'libxkbcommon-x11' 'cubeb' 'enet' 'libinih' 'discord-rpc' 'fmt' 'dynarmic' 'openal' 'cpp-httplib' 'libfdk-aac' 'crypto++')
makedepends=('git' 'rapidjson' 'cmake' 'python' 'clang' 'llvm' 'boost' 'qt6-tools' 'xbyak' 'cpp-jwt' 'qt6-wayland' 'desktop-file-utils' 'robin-map' 'glslang' 'spirv-headers' 'vulkan-memory-allocator')
provides=('citra')
conflicts=('citra')
source=("git+https://github.com/citra-emu/citra.git"
ext-lib.patch
gc.patch
fmt.patch)
md5sums=('SKIP'
         'ae871c25a1e593218a358b4366f19ae7'
         'f151510cd1fc233992710405cbf44323'
         'ed1cd1d3513e4961c9d1d8646f982b0e')

pkgver() {
	cd "$srcdir/$_pkgbase"
	echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_pkgbase"
    patch -p1 -i ../ext-lib.patch
    patch -p1 -i ../gc.patch
    patch -p1 -i ../fmt.patch
	mkdir -p build
    sed -i 's/OUTVAR ${BUILD_TAG}/OUTVAR "${BUILD_TAG}"/g' CMakeModules/GenerateSCMRev.cmake
    sed -i 's/OUTVAR ${BUILD_REPOSITORY}/OUTVAR "${BUILD_REPOSITORY}"/g' CMakeModules/GenerateBuildInfo.cmake
    sed -i 's|<json.hpp>|<nlohmann/json.hpp>|g' src/web_service/verify_login.cpp
    sed -i '1 i\#define CPPHTTPLIB_OPENSSL_SUPPORT' src/core/hle/service/http/http_c.h

    git submodule update --init --remote externals/lodepng
    git submodule update --init --remote externals/nihstro
    git submodule update --init --remote externals/soundtouch
    git submodule update --init --remote externals/sirit
    git submodule update --init --remote externals/teakra
    git submodule update --init --remote externals/dds-ktx
}

build() {
	# Trick the compiler into thinking we're building from a continuous
	# integration tool so the build number is correctly shown in the title
	cd "$srcdir/$_pkgbase"
	export CI=true
	export TRAVIS=true
	export TRAVIS_REPO_SLUG=citra-emu/citra
	export TRAVIS_TAG=$(git describe --tags)

    CXXFLAGS+=" -I/usr/include/libusb-1.0"
	mkdir -p build && cd build
	cmake .. \
	  -DCMAKE_INSTALL_PREFIX=/usr \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DENABLE_QT_TRANSLATION=ON \
      -DCITRA_ENABLE_COMPATIBILITY_REPORTING=ON \
      -DENABLE_COMPATIBILITY_LIST_DOWNLOAD=ON \
      -DUSE_DISCORD_PRESENCE=ON \
      -DENABLE_FFMPEG_VIDEO_DUMPER=ON \
      -DENABLE_FFMPEG_AUDIO_DECODER=ON \
      -DUSE_SYSTEM_BOOST=ON \
      -DUSE_SYSTEM_SDL2=ON \
      -DUSE_SYSTEM_OPENSSL=ON \
      -DUSE_SYSTEM_LIBUSB=ON \
      -DENABLE_MF=OFF \
      -DENABLE_TESTS=OFF \
      -DCITRA_CHECK_SUBMODULES=OFF \
      -DSIRIT_USE_SYSTEM_SPIRV_HEADERS=ON
    
    sed -i 's/ -lxbyak//g' src/citra_qt/CMakeFiles/citra-qt.dir/link.txt
    sed -i 's/ -lxbyak//g' src/citra/CMakeFiles/citra.dir/link.txt
	make
}

package() {
	cd "$srcdir/$_pkgbase/build"
	DESTDIR="$pkgdir/" make install
}
