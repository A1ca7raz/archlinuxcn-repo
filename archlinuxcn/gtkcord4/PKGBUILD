# Maintainer: txtsd <aur.archlinux@ihavea.quest>

pkgname=gtkcord4
pkgver=0.0.11
_pkgver=$(sed 's/\./-/3' <<<${pkgver})
pkgrel=1
pkgdesc="Discord client written in go and gtk4"
arch=('x86_64' 'aarch64')
url="https://github.com/diamondburned/gtkcord4"
license=('GPL3')
depends=('gtk4>=4.10.3' 'gobject-introspection' 'libadwaita>=1.3.2')
makedepends=('git' 'go>=1.20.3')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${_pkgver}.tar.gz")
sha256sums=('1708c599640deb6f7fb4d790808ceabb2f67078e4085b5a20732e101751473a9')

prepare() {
	mkdir -p "${pkgname}-${_pkgver}/build/"
}

build() {
	cd "${pkgname}-${_pkgver}/"

	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOPATH="${srcdir}/go"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -o build
}

package() {
	cd "${pkgname}-${_pkgver}/build/"
	install -Dm755 "${pkgname}" -t "${pkgdir}/usr/bin/"

	cd ..
	install -Dm644 "LICENSE.md" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	sed -i "s/Chat;/Chat;InstantMessaging;/" "nix/xyz.diamondb.${pkgname}.desktop"
	install -Dm644 "nix/xyz.diamondb.${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	install -Dm644 "xyz.diamondb.${pkgname}.metainfo.xml" "${pkgdir}/usr/share/metainfo/${pkgname}.metainfo.xml"
	install -Dm644 "internal/icons/png/logo.png" "${pkgdir}/usr/share/icons/hicolor/256x256/apps/${pkgname}.png"
	install -Dm644 "internal/icons/svg/logo.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.png"
}
