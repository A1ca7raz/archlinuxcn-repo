# Maintainer: Integral <integral@member.fsf.org>

pkgname=lemoa-git
_pkgname=${pkgname%-git}
pkgver=0.5.1.r3.gd0a1c2c
pkgrel=1
pkgdesc="Native Gtk client for Lemmy (alpha state)"
arch=('x86_64')
url="https://github.com/lemmy-gtk/${_pkgname}"
license=('GPL-3.0-or-later')
makedepends=('git' 'rust' 'gtk4' 'libadwaita')
depends=('gtk4' 'openssl' 'cairo')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"
	cargo build --release --target-dir target/
}

package() {
	cd "${_pkgname}/"
	install -vDm755 "target/release/${_pkgname}" -t "${pkgdir}/usr/bin/"
	install -vDm644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
