# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Zes4Null <rjw1248[at]hotmail[dot]com>
# Contributor: Philip <philip1731 protonmail ch>

pkgname=qucs-s
pkgver=24.1.0
pkgrel=1
pkgdesc="A spin-off of Qucs that supports other free SPICE circuit simulators like ngspice with the same Qucs GUI"
arch=('x86_64' 'i686')
url="https://ra3xdh.github.io"
license=('GPL-2.0-or-later')
depends=('glibc'
         'gcc-libs'
         'sh'
         'qt6-base'
         'qt6-svg'
         'ngspice'
         'python-numpy'
         'python-matplotlib'
         'hicolor-icon-theme'
         )
makedepends=('cmake'
             'qt6-tools'
             'ninja'
             )
optdepends=('qucs: Qucsator simulation backend'
            'xyce-serial: SPICE-compatible simulation backend'
            'spiceopus: general purpose simulation backend for optimization loops'
            'openvaf: compiler for Verilog-A devices support'
            'octave: high-level language for post-simulation data processing'
            )
source=(https://github.com/ra3xdh/qucs_s/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
b2sums=('73fbce57a2e76ca80edf29e1b711b1345d5f39c24d8d45288fc02a58e0d5eebdb02ab448ad05ab84fa030142ca6ca5384a033c264ce4c53615106fb1d29b5c73')

build() {
  cd $srcdir/$pkgname-$pkgver
  mkdir -p build
  cmake -S . -B build \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr
	
  ninja -C build
}

package() {
  cd $srcdir/$pkgname-$pkgver
  DESTDIR="$pkgdir" ninja -C build install
}
