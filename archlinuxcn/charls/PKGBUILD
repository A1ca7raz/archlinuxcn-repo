# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=charls
pkgver=2.4.2
pkgrel=2
pkgdesc='A C++ JPEG-LS library implementation'
arch=('x86_64')
url='https://github.com/team-charls/charls'
license=('BSD-3-Clause')
depends=(
  gcc-libs
  glibc
)
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/team-charls/charls/archive/${pkgver}.tar.gz")
sha512sums=('4f1b587f008956ab6fb9d2473c37a7b1a842633113245be7f8bb29b8c64304a6d580a29fcfca97ba1ac75adedbaf89e29adc4ac9e4117e1af1aa5949dbd34df9')

build() {
  cmake \
    -B "${pkgname}-${pkgver}/build" \
    -S "${pkgname}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -Wno-dev
  make -C "${pkgname}-${pkgver}/build"
}

check() {
  cd "${pkgname}-${pkgver}/build"
  ctest -v
}

package() {
  make -C "${pkgname}-${pkgver}/build" DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/${pkgname}-${pkgver}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
# for compatibility
  ln -s "/usr/include/charls" "${pkgdir}/usr/include/CharLS"
  ln -s "/usr/lib/libcharls.so" "${pkgdir}/usr/lib/libCharLS.so.2"
}
# vim:set ts=2 sw=2 et:

