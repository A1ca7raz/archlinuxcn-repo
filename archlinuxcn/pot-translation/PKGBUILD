# Maintainer: Pylogmon <pylogmon@outlook.com>

pkgname=pot-translation
_pkgname=pot-desktop
pkgver=2.2.1
_pkgver=$(sed 's/b/-beta/' <<<${pkgver})
pkgrel=1
pkgdesc="一个跨平台的划词翻译软件 | A cross-platform software for text translation."
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/pot-app/pot-desktop"
license=('GPL3')
depends=('webkit2gtk' 'gtk3' 'libayatana-appindicator' 'xdotool' 'libxcb' 'libxrandr' 'tesseract' 'tessdata')
makedepends=('nodejs' 'pnpm' 'rust' 'dbus' 'fnm')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${_pkgver}.tar.gz")
sha512sums=('537dded62ff6254d60d2684b85a828134e66ea69db4eebb8b58b3c54ec1a955d7e47c6c8c1936ddefc28329da04a1e93336f6ecde017444c051dd9196bb6e45f')
options=('!lto')

build() {
	fnm install 20.5.0
	eval "$(fnm env --use-on-cd --shell=bash)"
	fnm use 20.5.0

	cd "${_pkgname}-${_pkgver}/"
	sed -i "s/\"version\".*/\"version\": \"${_pkgver}\"/g" "src-tauri/tauri.conf.json"
	pnpm i
	pnpm tauri build -b deb
}

package() {
	cd "${_pkgname}-${_pkgver}/"
	tar -xpf "src-tauri/target/release/bundle/deb/pot_${_pkgver}_amd64/data.tar.gz" -C ${pkgdir}
	chown -R root:root ${pkgdir}
}
