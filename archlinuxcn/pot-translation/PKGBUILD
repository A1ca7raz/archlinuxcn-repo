# Maintainer: Pylogmon <pylogmon@outlook.com>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=pot-translation
_pkgname=pot-desktop
pkgver=3.0.5
_pkgver=${pkgver/b/-beta}
pkgrel=1
pkgdesc="一个跨平台的划词翻译软件 | A cross-platform software for text translation."
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/pot-app/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('webkit2gtk' 'gtk3' 'libayatana-appindicator' 'xdotool' 'libxcb' 'libxrandr' 'tesseract' 'tessdata')
makedepends=('nodejs' 'pnpm' 'cargo' 'dbus')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${_pkgver}.tar.gz")
sha512sums=('dfc4e59c80ba22f9481e8b65edbe69e5b2c42592abf3e542d9a14441fc7bd7b683180d38a0f3982f5f14b2350cf064cd11659e3a3b709dcfe114df1c1a97dff9')
options=('!lto')

prepare() {
	cd "${_pkgname}-${_pkgver}/"
	sed -i "s/\"version\".*/\"version\": \"${_pkgver}\"/g" "src-tauri/tauri.conf.json"
	pnpm i
}

build() {
	cd "${_pkgname}-${_pkgver}/"
	pnpm tauri build -b deb
}

package() {
	cd "${_pkgname}-${_pkgver}/"
	tar -xpf src-tauri/target/release/bundle/deb/pot_${_pkgver}_*/data.tar.gz -C "${pkgdir}"
	chown -R root:root "${pkgdir}"
}
