# Maintainer: Pylogmon <pylogmon@outlook.com>

pkgname=pot-translation
_pkgname=pot-desktop
pkgver=2.6.1
_pkgver=$(sed 's/b/-beta/' <<<${pkgver})
pkgrel=1
pkgdesc="一个跨平台的划词翻译软件 | A cross-platform software for text translation."
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/pot-app/pot-desktop"
license=('GPL3')
depends=('webkit2gtk' 'gtk3' 'libayatana-appindicator' 'xdotool' 'libxcb' 'libxrandr' 'tesseract' 'tessdata')
makedepends=('nodejs' 'pnpm' 'rust' 'dbus' 'fnm')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${_pkgver}.tar.gz")
sha512sums=('5ff605a0d868d6e3014becb92701f5d1fa83685e413bb22dae5f53b1f58f458b9b52d545f78083e5255045d5f015303359903be40d9eba04f4fd7eb8a52a0b3f')
options=('!lto')

build() {
	fnm install 20.5.0
	eval "$(fnm env --use-on-cd --shell=bash)"
	fnm use 20.5.0

	cd "${_pkgname}-${_pkgver}/"
	sed -i "s/\"version\".*/\"version\": \"${_pkgver}\"/g" "src-tauri/tauri.conf.json"
	pnpm i
	pnpm tauri build -b deb
}

package() {
	cd "${_pkgname}-${_pkgver}/"
	tar -xpf "src-tauri/target/release/bundle/deb/pot_${_pkgver}_amd64/data.tar.gz" -C ${pkgdir}
	chown -R root:root ${pkgdir}
}
