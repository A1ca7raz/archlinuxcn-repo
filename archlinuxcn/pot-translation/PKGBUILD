# Maintainer: Pylogmon <pylogmon@outlook.com>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=pot-translation
_pkgname=pot-desktop
pkgver=3.0.2
_pkgver=${pkgver/b/-beta}
pkgrel=1
pkgdesc="一个跨平台的划词翻译软件 | A cross-platform software for text translation."
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/pot-app/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('webkit2gtk' 'gtk3' 'libayatana-appindicator' 'xdotool' 'libxcb' 'libxrandr' 'tesseract' 'tessdata')
makedepends=('nodejs' 'pnpm' 'cargo' 'dbus')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${_pkgver}.tar.gz")
sha512sums=('d5bfd9fa536288711e7635a9ebe33bef3e55174aaf1731b5cd3a5611229fae83b963d691a0ceddefef387884b3e75e8639c00503c41458addc82834aedb48e35')
options=('!lto')

prepare() {
	cd "${_pkgname}-${_pkgver}/"
	sed -i "s/\"version\".*/\"version\": \"${_pkgver}\"/g" "src-tauri/tauri.conf.json"
	pnpm i
}

build() {
	cd "${_pkgname}-${_pkgver}/"
	pnpm tauri build -b deb
}

package() {
	cd "${_pkgname}-${_pkgver}/"
	tar -xpf "src-tauri/target/release/bundle/deb/pot_${_pkgver}_amd64/data.tar.gz" -C "${pkgdir}"
	chown -R root:root "${pkgdir}"
}
