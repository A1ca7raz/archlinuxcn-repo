# Maintainer: Pylogmon <pylogmon@outlook.com>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=pot-translation
_pkgname=pot-desktop
pkgver=3.0.4
_pkgver=${pkgver/b/-beta}
pkgrel=1
pkgdesc="一个跨平台的划词翻译软件 | A cross-platform software for text translation."
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/pot-app/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('webkit2gtk' 'gtk3' 'libayatana-appindicator' 'xdotool' 'libxcb' 'libxrandr' 'tesseract' 'tessdata')
makedepends=('nodejs' 'pnpm' 'cargo' 'dbus')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${_pkgver}.tar.gz")
sha512sums=('9194bc071be6fbe0a9e44cc7fba3c15fffdbd04d12745051bc4fb99ca611240fba8b5703a62d2fc68eef368a41266a03a180106720e52bf27029ac04c5465e38')
options=('!lto')

prepare() {
	cd "${_pkgname}-${_pkgver}/"
	sed -i "s/\"version\".*/\"version\": \"${_pkgver}\"/g" "src-tauri/tauri.conf.json"
	pnpm i
}

build() {
	cd "${_pkgname}-${_pkgver}/"
	pnpm tauri build -b deb
}

package() {
	cd "${_pkgname}-${_pkgver}/"
	tar -xpf "src-tauri/target/release/bundle/deb/pot_${_pkgver}_amd64/data.tar.gz" -C "${pkgdir}"
	chown -R root:root "${pkgdir}"
}
