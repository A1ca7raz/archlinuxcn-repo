# Maintainer: tohmais <callum.wishart@protonmail.com>

_pkgname="hyprland"
pkgname="${_pkgname}-nvidia-git"
pkgver=0.32.3.r23.g859841f
pkgrel=1
pkgdesc="A dynamic tiling Wayland compositor based on wlroots that doesn't sacrifice on its looks. (NVIDIA patch)"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/hyprwm/Hyprland"
license=('BSD')
depends=(
	libxcb
	xcb-proto
	xcb-util
	xcb-util-keysyms
	libxfixes
	libx11
	libxcomposite
	xorg-xinput
	libxrender
	pixman
	wayland-protocols
	cairo
	pango
	polkit
	glslang
	libinput
	libxcb
	libxkbcommon
	opengl-driver
	pixman
	wayland
	xcb-util-errors
	xcb-util-renderutil
	xcb-util-wm
	seatd
	vulkan-icd-loader
	vulkan-validation-layers
	xorg-xwayland
	libliftoff
	libdisplay-info
	libdisplay-info.so)
makedepends=(
	git
	cmake
	ninja
	meson
	vulkan-headers
	wayland-protocols
	xorgproto
	jq)
source=("${_pkgname}::git+https://github.com/hyprwm/Hyprland.git"
	"nvidia.patch")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
sha256sums=('SKIP'
	'c200d341641ee20a13b1893e27a9d823e9ef5ac2378e3cdecd0efc55a713db1c')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	cd "${_pkgname}/"
	git submodule update --init

	cd subprojects/wlroots/
	rm -rf build/
	patch -p1 <../packagefiles/wlroots-meson-build.patch
	patch --directory=. --forward --strip=0 --input="${srcdir}/nvidia.patch"
}

build() {
	cd "${_pkgname}/"

	meson setup build \
		--prefix /usr \
		--libexecdir lib \
		--sbindir bin \
		--buildtype plain \
		--wrap-mode nodownload \
		-D b_lto=true \
		-D b_pie=true \
		-D default_library=shared \
		-D xwayland=enabled

	ln -sf wlroots build/subprojects/wlroots/include/wlr
	meson compile -C build
}

package() {
	cd "${_pkgname}/"

	meson install -C build --destdir "${pkgdir}"

	rm -rf "${pkgdir}/usr/include/hyprland/wlroots/wlr/"
	ln -sf . "${pkgdir}/usr/include/hyprland/wlroots/wlr"

	# resolve conflicts with system wlr
	rm -f "${pkgdir}/usr/lib/libwlroots.so"
	rm -f "${pkgdir}/usr/lib/pkgconfig/wlroots.pc"

	# resolve conflicts with xdg-desktop-portal-hyprland from repo
	rm -rf "${pkgdir}/usr/share/xdg-desktop-portal/"
	rm -rf "${pkgdir}/usr/share/hyprland-protocols/"
	rm -rf "${pkgdir}/usr/share/pkgconfig/hyprland-protocols.pc"

	# FIXME: meson.build shall install version.h
	install -Dm644 src/version.h -t "${pkgdir}/usr/include/hyprland/src/"

	# License
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
