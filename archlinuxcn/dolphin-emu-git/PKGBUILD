# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lightning <sgsdxzy@gmail.com>

pkgbase=dolphin-emu-git
pkgname=('dolphin-emu-git' 'dolphin-emu-nogui-git')
pkgver=5.0.r19827.eb79bf37e20
pkgrel=1
pkgdesc='A GameCube / Wii / Triforce emulator'
arch=('x86_64' 'aarch64')
url='http://www.dolphin-emu.org/'
license=('GPL')
depends=(
  'alsa-lib' 'bluez-libs' 'cubeb' 'enet' 'hidapi' 'libevdev' 'libgl' 'libmgba'
  'libpulse' 'libspng' 'libx11' 'libxi' 'libxrandr' 'lzo' 'mbedtls' 'pugixml' 'qt6-base'
  'sfml' 'zlib' 'minizip-ng' 'sdl2' 'bzip2' 'xz' 'zstd' 'libspng' 'qt6-svg'
  'libcurl.so' 'ffmpeg' 'fmt' 'sfml' 'miniupnpc' 'curl' 'libiconv' 
  'libminiupnpc.so' 'libudev.so' 'libusb-1.0.so' 'libxxhash.so'
)
makedepends=('cmake' 'git' 'libglvnd' 'python' 'ninja' 'gtest')
optdepends=('pulseaudio: PulseAudio backend')
options=('!emptydirs')
source=('dolphin-emu::git+https://github.com/dolphin-emu/dolphin.git')
sha256sums=('SKIP')

pkgver() {
  cd dolphin-emu

  git describe | sed 's/-/.r/; s/-g/./'
}

prepare() {
  cd dolphin-emu
  git submodule update --init --recursive

  sed -i '1 i #include<cstdio>' Externals/VulkanMemoryAllocator/include/vk_mem_alloc.h
  sed -i 's/minizip>=3.0.0/minizip-ng>=3.0.0/g' CMakeLists.txt
  sed -i 's/currentVideoSize/desiredVideoDimensions/g' Source/Core/Core/HW/GBACore.cpp
}

build() {
  cd dolphin-emu

  cmake -S . -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DENABLE_QT='TRUE' \
    -DUSE_SHARED_ENET='TRUE' \
    -DXXHASH_FOUND='TRUE'\
    -DUSE_DISCORD_RPC_PRESENCE='TRUE' \
    -DUSE_RETRO_ACHIEVEMENTS='TRUE' \
    -DDISTRIBUTOR='archlinuxcn.org'
  cmake --build build/
}

package_dolphin-emu-git() {
  provides=('dolphin-emu')
  conflicts=('dolphin-emu')

  cd dolphin-emu
  DESTDIR="$pkgdir" cmake --install 'build/'
  rm "${pkgdir}"/usr/bin/dolphin-emu-nogui
  rm -rf "${pkgdir}"/usr/{include,lib/libdiscord-rpc.a}
  rm -rf "$pkgdir/usr/share/man/man6/dolphin-emu-nogui.6"
  install -Dm 644 Data/51-usb-device.rules -t "${pkgdir}"/usr/lib/udev/rules.d/
}

package_dolphin-emu-nogui-git() {
  depends=('dolphin-emu-git')
  provides=("dolphin-emu-nogui" "dolphin-emu-cli")
  conflicts=("dolphin-emu-nogui" "dolphin-emu-cli")

  cd dolphin-emu
  install -dm 755 "${pkgdir}"/usr/bin
  install -m 755 build/Binaries/dolphin-emu-nogui "${pkgdir}"/usr/bin/dolphin-emu-nogui
  ln -sf /usr/bin/dolphin-emu-nogui "${pkgdir}"/usr/bin/dolphin-emu-cli
  install -Dm644 "Data/dolphin-emu-nogui.6" "$pkgdir/usr/share/man/man6/dolphin-emu-nogui.6"
}

# vim: ts=2 sw=2 et:
