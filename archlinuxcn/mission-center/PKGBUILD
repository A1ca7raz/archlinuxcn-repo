# Maintainer: Mark Wagie <mark dot wagie at proton dot me>

pkgname=mission-center
pkgver=0.4.4.2
_pkgver=$(sed 's/\./-/3' <<<${pkgver})
pkgrel=1
pkgdesc="Monitor your CPU, Memory, Disk, Network and GPU usage"
arch=('x86_64')
url="https://gitlab.com/mission-center-devs/${pkgname}"
license=('GPL-3.0-or-later')
depends=('dmidecode' 'libadwaita' 'nvtop')
makedepends=('blueprint-compiler' 'cargo' 'meson')
checkdepends=('appstream-glib')
options=('!lto')
source=("${url}/-/archive/v${_pkgver}/${pkgname}-v${_pkgver}.tar.gz")
sha256sums=('fa3f23c44eb7a0ef522567f579b0c8c611f6bfeda03755e96867a19c62155def')

prepare() {
	cd "${pkgname}-v${_pkgver}/"
	export CARGO_HOME="$srcdir/cargo-home"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
	export CARGO_HOME="${srcdir}/cargo-home"
	export RUSTUP_TOOLCHAIN=stable
	arch-meson "${pkgname}-v${_pkgver}" build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs || :
}

package() {
	meson install -C build --no-rebuild --destdir "${pkgdir}"
}
